*==================================================================================================                                                                                                                                                             
* Cliente : AMCO SpA                                                                                                                                                                                                                                            
* Office  : CRO Portfolio Managment -> Data Verification                                                                                                                                                                                                        
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* Macro name  : hx_list_dataset_created                                                                                                                                                                                                                         
* Description : Retrieve all dataset created on any libname                                                                                                                                                                                                     
* Author      : Uriele De Piano Hexe SpA, 18 October 2020                                                                                                                                                                                                       
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* NOTE: Macro is divided in two component                                                                                                                                                                                                                       
*	                                                                                                                                                                                                                                                              
*==================================================================================================                                                                                                                                                             
;                                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                
%Macro hx_list_dataset_created(libname  = _NULL_                                                                                                                                                                                                                
														  ,dataList = hx_list_dataset_created                                                                                                                                                                                                             
														 ) / store secure;                                                                                                                                                                                                                                               
	%Local folderDati                                                                                                                                                                                                                                              
		;                                                                                                                                                                                                                                                             
	%Let timeStamp  = %sysfunc(putn(%sysfunc(datetime()),datetime.));                                                                                                                                                                                              
	%Let folderDati = %sysfunc(pathName(&libname.));                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                
	%Put +----[macro=&sysmacroname.] --------------------------------+;                                                                                                                                                                                            
	%Put | Retrieve list of dataset created                          |;                                                                                                                                                                                            
	%Put |    Libname    : &libname.												 				 |;                                                                                                                                                                                                           
	%Put |    folderDati : &folderDati.													 		 |;                                                                                                                                                                                                         
	%Put |...........................................................|;                                                                                                                                                                                            
	%Put | Started at: &timeStamp.																	 |;                                                                                                                                                                                                             
	%Put +-----------------------------------------------------------+;                                                                                                                                                                                            
                                                                                                                                                                                                                                                                
	Data _listOfTableDatiOdd                                                                                                                                                                                                                                       
				(Keep=folderDati Dbcontainer TableContainer tableAlias tableName attrFile                                                                                                                                                                                   
							crDte timeStamp tmpStampInterval);                                                                                                                                                                                                                       
		Attrib 	Dbcontainer			Length=$500                                                                                                                                                                                                                             
						TableContainer	Length=$100                                                                                                                                                                                                                                
						tableAlias			Length=$80	                                                                                                                                                                                                                                  
						tableName   		Length=$40                                                                                                                                                                                                                                  
						crDte 					Length=8		format=datetime.                                                                                                                                                                                                                     
						_hcrDte					Length=8	                                                                                                                                                                                                                                     
						_mcrDte					Length=8	                                                                                                                                                                                                                                     
						timeStamp	  		Length=8		format=datetime.                                                                                                                                                                                                                  
						folderDati			Length=$400                                                                                                                                                                                                                                  
						attrFile				Length=$256	                                                                                                                                                                                                                                  
			;                                                                                                                                                                                                                                                            
	  Retain slash folderDati ' ' timeStamp .;                                                                                                                                                                                                                     
		Set &nplSourceList. (Rename=(tableName=TableContainer)Where=(Upcase(DataProvider)="%Upcase(&dataProvider.)"                                                                                                                                                   
		%If %symExist(dta_reference) %Then %Do;                                                                                                                                                                                                                       
			And (dtaValid_from<="&dta_reference."d And dtaValid_to>="&dta_reference."d)                                                                                                                                                                                  
		%End;                                                                                                                                                                                                                                                         
		));                                                                                                                                                                                                                                                           
		If _N_=1 Then Do;                                                                                                                                                                                                                                             
			slash 		 = ifc(symget("sysscp")=:"WIN",'\','/');                                                                                                                                                                                                             
			folderDati = symget("folderDati");                                                                                                                                                                                                                           
			timeStamp  = datetime();                                                                                                                                                                                                                                     
		End;                                                                                                                                                                                                                                                          
		%*-- Check Last dataset created;                                                                                                                                                                                                                              
		tableAlias = Ifc(not missing(tableAlias)                                                                                                                                                                                                                      
										 ,tableAlias                                                                                                                                                                                                                                          
										 ,compress(translate(tableName,' ','$'))                                                                                                                                                                                                              
										 );                                                                                                                                                                                                                                                   
		cmdWin = 'dir /O-D "'||strip(folderDati)||strip(slash)||strip(tableAlias)||'*.sas7bdat"';                                                                                                                                                                     
		rc     = filename("dlist",cmdWin,"pipe");                                                                                                                                                                                                                     
		fid    = fopen("dlist",'s');                                                                                                                                                                                                                                  
		Do While (Fread(fid)=0);                                                                                                                                                                                                                                      
			rc = fget(fid,attrFile,256);                                                                                                                                                                                                                                 
			If prxmatch("/sas7bdat/i",attrFile)>0 Then Do;                                                                                                                                                                                                               
				_tmtStampWkb = input(scan(compbl(attrFile),1,' '),anydtdte21.);                                                                                                                                                                                             
				_htm  		 	 = scan(compbl(attrFile),2,' ');		                                                                                                                                                                                                                
				_hcrDte			 = Input(scan(_htm,1,':'),2.);                                                                                                                                                                                                                    
				_tmpStmpFull = catx(' ','/',scan(compbl(attrFile),2,' ')," PM/i");                                                                                                                                                                                          
				*-- Put _tmpStmpFull=;                                                                                                                                                                                                                                      
				If prxmatch(_tmpStmpFull,attrFile)>0 Then Do;                                                                                                                                                                                                               
					_hcrDte = sum(_hcrDte,12);                                                                                                                                                                                                                                 
					*-- Put _hcrDte=;                                                                                                                                                                                                                                          
				End;                                                                                                                                                                                                                                                        
				_mcrDte			 = Input(scan(_htm,2,':'),2.);                                                                                                                                                                                                                    
				crDte            = dhms(_tmtStampWkb,_hcrDte,_mcrDte,0);                                                                                                                                                                                                    
				tmpStampInterval = intck("minute",timeStamp,crDte);                                                                                                                                                                                                         
				tableName        = scan(scan(attrFile,1,' ','b'),1,'.');                                                                                                                                                                                                    
				*- Il nome completo del dataset contiene un numero che è il numero di riga                                                                                                                                                                                 
				   all interno del Metadata NPL_SOURCE_LIST.                                                                                                                                                                                                                
				   Il nome è composto da TableAlias concatenato con il numero                                                                                                                                                                                              
						;                                                                                                                                                                                                                                                         
				*-- Ricerco inizio del numero;                                                                                                                                                                                                                              
				findDigit = anydigit(tableName);                                                                                                                                                                                                                            
				If findDigit>0 Then Do;                                                                                                                                                                                                                                     
					If Lowcase(Substr(tableName,1,findDigit-2)) = Lowcase(TableAlias) Then Output;                                                                                                                                                                             
				End;                                                                                                                                                                                                                                                        
				Else Output;                                                                                                                                                                                                                                                
			End;                                                                                                                                                                                                                                                         
		End;                                                                                                                                                                                                                                                          
		fid = Fclose(fid);                                                                                                                                                                                                                                            
	Run;                                                                                                                                                                                                                                                           
	Proc Sort data=_listOfTableDatiOdd;                                                                                                                                                                                                                            
		by tableAlias tmpStampInterval;                                                                                                                                                                                                                               
	Run;                                                                                                                                                                                                                                                           
	Data &dataList.; Set _listOfTableDatiOdd;                                                                                                                                                                                                                      
		by tableAlias tmpStampInterval;                                                                                                                                                                                                                               
		If first.tableAlias Then Output;                                                                                                                                                                                                                              
	Run;                                                                                                                                                                                                                                                           
%Mend;                                                                                                                                                                                                                                                          
