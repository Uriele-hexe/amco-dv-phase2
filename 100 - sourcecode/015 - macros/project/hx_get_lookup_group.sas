*==================================================================================================                                                                                                                                                             
* Cliente : AMCO SpA                                                                                                                                                                                                                                            
* Office  : CRO Portfolio Managment -> Data Verification                                                                                                                                                                                                        
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* Macro name  : hx_get_lookup_group                                                                                                                                                                                                                             
* Description : Execute all the lookup rules belong at each id lookup group                                                                                                                                                                                     
* Author      : Uriele De Piano Hexe SpA, 25 November 2020                                                                                                                                                                                                      
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* Parameter:                                                                                                                                                                                                                                                    
*    1) idLookupGroup : Id lookup group                                                                                                                                                                                                                         
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* Output                                                                                                                                                                                                                                                        
*  - Dataset defined into metadata that describe lookup.                                                                                                                                                                                                        
*  It is recommended that you create a single dataset in the staging area for each checklist group                                                                                                                                                              
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* NOTE: Statment about hash table will be write in dataset sas                                                                                                                                                                                                  
*				Macro update process trace, too.                                                                                                                                                                                                                           
*==================================================================================================                                                                                                                                                             
;                                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                
%Macro hx_get_lookup_group(dsMtd=_null_,idLookupGroup=_null_ ) / store secure ;                                                                                                                                                                                 
	%local _dsmtd _idLookup _timeLookupStamp                                                                                                                                                                                                                       
			;                                                                                                                                                                                                                                                            
	%*-- UDP: Check existence dsMTD;                                                                                                                                                                                                                               
	%If %sysfunc(exist(&dsMtd.))=0 %Then %Do;                                                                                                                                                                                                                      
  	%log(level = E, msg = &dsMTD. not exists);                                                                                                                                                                                                                   
    %return;                                                                                                                                                                                                                                                    
	%End;                                                                                                                                                                                                                                                          
	%Let _timeLookupStamp = %sysfunc(datetime());                                                                                                                                                                                                                  
  %* Apro tabella metadati + controllo operazione;                                                                                                                                                                                                              
  %let _dsmtd = %sysfunc(open(&dsMtd. (where=( upcase(id_Lookup_Group) = upcase("&idLookupGroup.") ))));                                                                                                                                                        
  %if %sysevalf(&_dsmtd. = 0, boolean) %then %do;                                                                                                                                                                                                               
      %log(level = E, msg = Cannot open dset &dsMtd. with statement: %nrstr(%let _dsmtd = %sysfunc(open(&dsMtd. (where=( upcase(id_Lookup) = upcase("&idLookup.") ))));));                                                                                      
			%let _dsmtd = %sysfunc(close(&_dsid.));                                                                                                                                                                                                                      
      %return;                                                                                                                                                                                                                                                  
  %end;                                                                                                                                                                                                                                                         
	%Do %While (%sysfunc(fetch(&_dsMtd.))=0);                                                                                                                                                                                                                      
		%Let _idLookup = %sysfunc(getvarc(&_dsMtd.,%sysfunc(varnum(&_dsmtd.,Id_lookup))));                                                                                                                                                                            
		%hx_join_from_mtd(&dsMtd., %NrQuote(&_idLookup.));                                                                                                                                                                                                            
	%End;                                                                                                                                                                                                                                                          
	%Let _dsmtd = %sysfunc(close(&_dsmtd.));                                                                                                                                                                                                                       
                                                                                                                                                                                                                                                                
	%*-- Extract final dataset from metadata, to update process trace;                                                                                                                                                                                             
	%Let tableOut = _NULL_;                                                                                                                                                                                                                                        
	Data _null_;                                                                                                                                                                                                                                                   
		_timeLookupStamp = &_timeLookupStamp.;                                                                                                                                                                                                                        
		%hx_declare_ht_pr(htTable=&processTrace.);                                                                                                                                                                                                                    
		idLookupGroup = strip(symget("idLookupGroup"));                                                                                                                                                                                                               
		sourceCode    = symget("sysmacroname");                                                                                                                                                                                                                       
		stepCode      = catx(' ',"Esecuzione del gruppo lookup:",idLookupGroup);                                                                                                                                                                                      
		Call Missing(rcCode,msgCode);	                                                                                                                                                                                                                                
		dsMtd = open(cats("&dsMtd. (Where=(id_Lookup_Group='",idLookupGroup,"' And Upcase(Scan(Target_Out,1,'.')) ^= 'WORK'))"));                                                                                                                                     
                                                                                                                                                                                                                                                                
		Do While (Fetch(dsMtd)=0);                                                                                                                                                                                                                                    
			tableNameOut = GetvarC(dsMtd,Varnum(dsMtd,"Target_Out"));                                                                                                                                                                                                    
			modTmps 		 = hx_get_crdte(tableNameOut);                                                                                                                                                                                                                     
			Put tableNameOut= modTmps=datetime. _timeLookupStamp=datetime.;                                                                                                                                                                                              
			If modTmps>_timeLookupStamp Then Do;                                                                                                                                                                                                                         
				rcCode  = 1;                                                                                                                                                                                                                                                
				msgCode = cats("Creazione della tabella [",tableNameOut,']');                                                                                                                                                                                               
				Call Symput("tableOut",Strip(Scan(tableNameOut,2,'.')));                                                                                                                                                                                                    
				rc = ht.add();                                                                                                                                                                                                                                              
			End;                                                                                                                                                                                                                                                         
		End;                                                                                                                                                                                                                                                          
		dsMtd = Close(dsMtd);                                                                                                                                                                                                                                         
		rc = ht.output(dataset:"&processTrace.");                                                                                                                                                                                                                     
	Run;                                                                                                                                                                                                                                                           
	%hx_update_tabletrace(datistg,trasformation                                                                                                                                                                                                                    
 			                   ,whrcl=%NrQuote(Upcase%(MEMNAME%) like "&tableOut._%%"));                                                                                                                                                                                
%Mend;                                                                                                                                                                                                                                                          
