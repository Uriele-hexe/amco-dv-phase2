                                                                                                                                                                                                                                                                
%macro hx_sql_join(                                                                                                                                                                                                                                             
    dsout=, dsoutCols=,                                                                                                                                                                                                                                         
    dsin=, dsinCols=, dsinWhere=, dsinKeys=, joinType=,                                                                                                                                                                                                         
    dsLkp=, dsLkpWhere=, dsLkpKeys=,                                                                                                                                                                                                                            
    idLookup=                                                                                                                                                                                                                                                   
) / store secure ;                                                                                                                                                                                                                                                              
                                                                                                                                                                                                                                                                
    %local                                                                                                                                                                                                                                                      
        _i _col _colLkp _sqlRc _sqlMsg                                                                                                                                                                                                                          
    ;                                                                                                                                                                                                                                                           
                                                                                                                                                                                                                                                                
    proc sql;                                                                                                                                                                                                                                                   
        create table &dsout. as                                                                                                                                                                                                                                 
        select                                                                                                                                                                                                                                                  
            %* Recupero e scrivo nel SQL le colonne della tabella di input;                                                                                                                                                                                     
            %let _i = 1;                                                                                                                                                                                                                                        
            %let _col = %qscan(%bquote(&dsinCols.), &_i., %str(-));                                                                                                                                                                                             
            %unquote(in.&_col.)                                                                                                                                                                                                                                 
            %let _i = 2;                                                                                                                                                                                                                                        
            %let _col = %qscan(%bquote(&dsinCols.), &_i., %str(-));                                                                                                                                                                                             
            %do %while(%bquote(&_col.)^=);                                                                                                                                                                                                                      
                ,in.&_col.                                                                                                                                                                                                                                      
                %let _i = %eval(&_i.+1);                                                                                                                                                                                                                        
                %let _col = %qscan(%bquote(&dsinCols.), &_i., %str(-));                                                                                                                                                                                         
            %end;                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                
            %* Recupero e scrivo nel SQL le colonne della tabella di lookup;                                                                                                                                                                                    
						%if %is_empty(%bquote(&dsoutCols.))=0 %then %do;                                                                                                                                                                                                          
	            %let _i = 1;                                                                                                                                                                                                                                       
 	           %let _col = %qscan(%bquote(&dsoutCols.), &_i., %str(-));                                                                                                                                                                                           
  	          %do %while(%bquote(&_col.)^=);                                                                                                                                                                                                                     
   	             ,lkp.&_col.                                                                                                                                                                                                                                    
    	            %let _i = %eval(&_i.+1);                                                                                                                                                                                                                       
     	           %let _col = %qscan(%bquote(&dsoutCols.), &_i., %str(-));                                                                                                                                                                                       
      	      %end;                                                                                                                                                                                                                                              
						%end;                                                                                                                                                                                                                                                     
            %* Creo flag aggancio;                                                                                                                                                                                                                              
            ,ifc(lkp.%qscan(%bquote(&dsLkpKeys.), 1, %str(-)) is null, 'N', 'Y') as flglkp_%replace_spec_chars(&idLookup.) length=1 format=$1. "Lookup on &dsLkp."                                                                                              
                                                                                                                                                                                                                                                                
        from &dsin. (where=(&dsinWhere.)) as in                                                                                                                                                                                                                 
        &joinType. &dsLkp. (where=(&dsLkpWhere.)) as lkp                                                                                                                                                                                                        
        on                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
        /* Recupero e scrivo nel SQL le chiavi di aggancio;*/                                                                                                                                                                                                   
        %let _i = 1;                                                                                                                                                                                                                                            
        %let _key = %qscan(%bquote(&dsinKeys.), &_i., %str(-));                                                                                                                                                                                                 
        %let _keyLkp = %qscan(%bquote(&dsLkpKeys.), &_i., %str(-));                                                                                                                                                                                             
        in.%UnQuote(&_key.) = lkp.%UnQuote(&_keyLkp.)                                                                                                                                                                                                           
        %let _i = 2;                                                                                                                                                                                                                                            
        %let _key = %qscan(%bquote(&dsinKeys.), &_i.,%str(-));                                                                                                                                                                                                  
        %let _keyLkp = %qscan(%bquote(&dsLkpKeys.), &_i.,%str(-));                                                                                                                                                                                              
        %do %while(%bquote(&_key.)^=);                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
            and in.%UnQuote(&_key.) = lkp.%UnQuote(&_keyLkp.)                                                                                                                                                                                                   
                                                                                                                                                                                                                                                                
            %let _i = %eval(&_i.+1);                                                                                                                                                                                                                            
            %let _key = %qscan(%bquote(&dsinKeys.), &_i., %str(-));                                                                                                                                                                                             
            %let _keyLkp = %qscan(%bquote(&dsLkpKeys.), &_i., %str(-));                                                                                                                                                                                         
        %end;                                                                                                                                                                                                                                                   
                                                                                                                                                                                                                                                                
        ;                                                                                                                                                                                                                                                       
    quit;                                                                                                                                                                                                                                                       
		%Let _sqlRc  = &sqlRc.;                                                                                                                                                                                                                                       
		%Let _sqlMsg = &syserrortext.;                                                                                                                                                                                                                                
		%if &_sqlRc.<=4 %Then %Do;                                                                                                                                                                                                                                    
			%*-- Add new variable named osslkp_<rule>;                                                                                                                                                                                                                   
			%Let _nchiavi = %sysfunc(count(&dsinKeys.,%NrQuote(-)));                                                                                                                                                                                                     
			%Let _nchiavi = %Eval(&_nchiavi.+1);                                                                                                                                                                                                                         
			Data &dsout.; Set &dsout.;                                                                                                                                                                                                                                   
				Attrib osslkp_%replace_spec_chars(&idLookup.) Length=8 label="Sequence lookup on &dsLkpKeys.";                                                                                                                                                              
				By                                                                                                                                                                                                                                                          
				%Do _i=1 %To &_nchiavi.;                                                                                                                                                                                                                                    
					%Let _chiave = %Qscan(%NrQuote(&dsinKeys.),&_i.,%NrQuote(-));                                                                                                                                                                                              
					&_chiave.                                                                                                                                                                                                                                                  
			  %End;                                                                                                                                                                                                                                                      
					;                                                                                                                                                                                                                                                          
					%Put &=_chiave.;                                                                                                                                                                                                                                           
                                                                                                                                                                                                                                                                
				Retain osslkp_%replace_spec_chars(&idLookup.) 0;                                                                                                                                                                                                            
				If first.&_chiave. then Call Missing(osslkp_%replace_spec_chars(&idLookup.));                                                                                                                                                                               
				If flglkp_%replace_spec_chars(&idLookup.)='Y' Then                                                                                                                                                                                                          
					osslkp_%replace_spec_chars(&idLookup.) = sum(osslkp_%replace_spec_chars(&idLookup.),1);                                                                                                                                                                    
			Run;                                                                                                                                                                                                                                                         
		%End;                                                                                                                                                                                                                                                         
		%*-- Update Process Trace and Table Trace;                                                                                                                                                                                                                    
		%*-- Change UDP 30 November 2020;                                                                                                                                                                                                                             
		Data _null_;                                                                                                                                                                                                                                                  
			%*-- Declare hash table regarding process trace;                                                                                                                                                                                                             
			idLookup = Strip(Symget("idLookup"));                                                                                                                                                                                                                        
			%hx_declare_ht_pr(htTable=&processTrace.);                                                                                                                                                                                                                   
			sourceCode    = symget("sysmacroname");                                                                                                                                                                                                                      
			stepCode      = catx(' ',"Esecuzione della lookup:",idLookup,"method: &joinType.");                                                                                                                                                                          
			Call Missing(rcCode,msgCode);                                                                                                                                                                                                                                
			dsOutput = "&dsout.";                                                                                                                                                                                                                                        
			If exist(dsOutput) Then Do;                                                                                                                                                                                                                                  
				nrec 		= hx_get_nobs(dsOutput);                                                                                                                                                                                                                             
				msgCode = catx(' ',dsOutput,"has been created with",put(nrec,commax18.),"records");                                                                                                                                                                         
				rcCode  = 1;                                                                                                                                                                                                                                                
			End;                                                                                                                                                                                                                                                         
			Else Do;                                                                                                                                                                                                                                                     
				msgCode = catx(' ',dsOutput,"has not been created.",Strip(Symget("_sqlMsg")));                                                                                                                                                                              
				rcCode  = symget("_sqlRc");;                                                                                                                                                                                                                                
			End;                                                                                                                                                                                                                                                         
			rc = ht.output(dataset:"&processTrace.");                                                                                                                                                                                                                    
		Run;                                                                                                                                                                                                                                                          
%mend hx_sql_join;                                                                                                                                                                                                                                              
