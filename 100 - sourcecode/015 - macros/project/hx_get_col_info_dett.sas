%macro hx_get_col_info_dett(dsIn, dsOut, dsInWhere=1, sepCampi=%str(-), colCharLen=50, maxNcolsDett=10, outputLimit=5000) / store secure;
                                                                                                                                                                                                                                                                
    %local i j _i;                                                                                                                                                                                                                                              
                                                                                                                                                                                                                                                                
	%if %is_empty(%bquote(&dsIn.)) %then %do;                                                                                                                                                                                                                      
		%log(level=E, msg= Parametro dsIn non inserito);                                                                                                                                                                                                              
	%end;                                                                                                                                                                                                                                                          
	%if %is_empty(%bquote(&dsOut.)) %then %do;                                                                                                                                                                                                                     
		%log(level=E, msg= Parametro dsOut non inserito);                                                                                                                                                                                                             
	%end;                                                                                                                                                                                                                                                          
	%if %sysfunc(exist(&dsIn.))=0 %then %do;                                                                                                                                                                                                                       
		%log(level=E, msg= La tabella &dsIn. non esiste);                                                                                                                                                                                                             
	%end;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
    data &dsOut. (drop= dsid _msg i _var);                                                                                                                                                                                                                      
		length _var $32 _msg $256;                                                                                                                                                                                                                                    
        set &dsIn. (where=(%unquote(&dsInWhere.)));                                                                                                                                                                                                             
		attrib                                                                                                                                                                                                                                                        
			%do i = 1 %to &maxNcolsDett.;                                                                                                                                                                                                                                
				col_&i. length=$&colCharLen. format=$&colCharLen..                                                                                                                                                                                                          
			%end;                                                                                                                                                                                                                                                        
		;                                                                                                                                                                                                                                                             
		call missing(_var                                                                                                                                                                                                                                             
			%do j = 1 %to &maxNcolsDett.;                                                                                                                                                                                                                                
				,col_&j.                                                                                                                                                                                                                                                    
			%end;                                                                                                                                                                                                                                                        
		);                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                                
		if missing(perimetro_di_applicabilita) then perimetro_di_applicabilita='1';                                                                                                                                                                                   
                                                                                                                                                                                                                                                                
        dsid = open(tableName || "(where=(" || perimetro_di_applicabilita || " and " || upcase(flagName) || "='Y'))");                                                                                                                                          
		if dsid <= 0 then do;                                                                                                                                                                                                                                         
			_msg = sysmsg();                                                                                                                                                                                                                                             
			put _msg;                                                                                                                                                                                                                                                    
			stop;                                                                                                                                                                                                                                                        
		end;                                                                                                                                                                                                                                                          
        i = 0;                                                                                                                                                                                                                                                  
        do while(fetch(dsid)=0 and i <= &outputLimit.);                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
			_var = 'idRecord';                                                                                                                                                                                                                                           
			if varnum(dsid, _var) <= 0 then link msg_not_exist_var;                                                                                                                                                                                                      
			numero_riga = getvarn(dsid, varnum(dsid, _var));                                                                                                                                                                                                             
			                                                                                                                                                                                                                                                             
			%do _i = 1 %to &maxNcolsDett.;                                                                                                                                                                                                                               
				_var = scan(campi_tecnici, &_i., '-');                                                                                                                                                                                                                      
				if not missing(_var) then do;                                                                                                                                                                                                                               
					if varnum(dsid, _var) <= 0 then link msg_not_exist_var;                                                                                                                                                                                                    
					if vartype(dsid, varnum(dsid, _var)) = 'C' then col_&_i. = getvarc(dsid, varnum(dsid, _var));                                                                                                                                                              
					else col_&_i. = put(getvarn(dsid, varnum(dsid, _var)), 32.);                                                                                                                                                                                               
				end;                                                                                                                                                                                                                                                        
			%end;                                                                                                                                                                                                                                                        
                                                                                                                                                                                                                                                                
            i+1;                                                                                                                                                                                                                                                
            output;                                                                                                                                                                                                                                             
        end;                                                                                                                                                                                                                                                    
        dsid = close(dsid);                                                                                                                                                                                                                                     
	return;                                                                                                                                                                                                                                                        
                                                                                                                                                                                                                                                                
	msg_not_exist_var:                                                                                                                                                                                                                                             
		_msg = 'ERROR: Variabile ' || _var || ' non esiste sulla tabella di dettaglio ' || tableName;                                                                                                                                                                 
		put _msg;                                                                                                                                                                                                                                                     
		stop;                                                                                                                                                                                                                                                         
	return;                                                                                                                                                                                                                                                        
                                                                                                                                                                                                                                                                
    run;                                                                                                                                                                                                                                                        
                                                                                                                                                                                                                                                                
%mend hx_get_col_info_dett;                                                                                                                                                                                                                                     
