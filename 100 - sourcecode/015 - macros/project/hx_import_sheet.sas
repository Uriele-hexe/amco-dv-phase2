*============================================================================================                                                                                                                                                                   
* Client Name: AMCO SpA                                                                                                                                                                                                                                         
* Project    : Framework di Data Verification                                                                                                                                                                                                                   
*--------------------------------------------------------------------------------------------                                                                                                                                                                   
* Macro name  : hx_import_sheet                                                                                                                                                                                                                                 
* Author      : Uriele De Piano Hexe SpA 02 October 2020                                                                                                                                                                                                        
* Description : Import metadata from excel workbook or sheet in dataset sas                                                                                                                                                                                     
*--------------------------------------------------------------------------------------------                                                                                                                                                                   
* NOTE: If parameter wkbSheet is empty the proc import uses first no empty sheet                                                                                                                                                                                
*============================================================================================                                                                                                                                                                   
;                                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                
%Macro hx_import_sheet (wkbFolder=,wkbName=,wkbSheet=_NULL_,dsImport=hx_import_sheet,startRow=1 ) / store secure ;                                                                                                                                              
	%local slash check fullName tmpStamp                                                                                                                                                                                                                           
		;                                                                                                                                                                                                                                                             
	                                                                                                                                                                                                                                                               
	%Let tmpStamp = %sysfunc(putn(%sysfunc(datetime()),datetime.));                                                                                                                                                                                                
  %Put +--[Macro: &sysmacroname.] -----------------------------------------------;                                                                                                                                                                              
	%Put | Import a Workbook / SheetName;                                                                                                                                                                                                                          
	%Put | WorkBook Folder : &wkbFolder.;                                                                                                                                                                                                                          
	%Put | WorkBook Name   : &wkbName.;                                                                                                                                                                                                                            
	%Put | Sheet Name      : &wkbSheet.;                                                                                                                                                                                                                           
	%Put |.........................................................................;                                                                                                                                                                               
	%Put | Start time : &tmpStamp.;                                                                                                                                                                                                                                
  %Put +-------------------------------------------------------------------------;                                                                                                                                                                              
                                                                                                                                                                                                                                                                
	%Let check = OK;                                                                                                                                                                                                                                               
	Data _null_;                                                                                                                                                                                                                                                   
	  slash = ifc(Symget("SYSSCP")="WIN",'\','/');                                                                                                                                                                                                                 
		fullName = strip("&wkbFolder.")||strip(slash)||strip("&wkbName.");                                                                                                                                                                                            
		call symput("fullName",strip(fullName));                                                                                                                                                                                                                      
		if not fileExist(fullName) then call symput("check","KO");                                                                                                                                                                                                    
 	Run;                                                                                                                                                                                                                                                          
	%if &check.=KO %then %do;                                                                                                                                                                                                                                      
		%Put +--[&sysmacroname: ERROR] -------------------------------;                                                                                                                                                                                               
		%Put | Workbook file: %UnQuote(&fullName.) not exists         ;                                                                                                                                                                                               
		%Put +--------------------------------------------------------;                                                                                                                                                                                               
		%return;                                                                                                                                                                                                                                                      
	%end;                                                                                                                                                                                                                                                          
	proc import out= &dsImport.                                                                                                                                                                                                                                    
	    datafile = "&fullName."                                                                                                                                                                                                                                    
	    dbms = Excel;                                                                                                                                                                                                                                              
			%if %Upcase(&wkbSheet.) ne _NULL_ %then %do;                                                                                                                                                                                                                 
	    	sheet = "&wkbSheet.";                                                                                                                                                                                                                                     
			%end;                                                                                                                                                                                                                                                        
	    getnames = yes;                                                                                                                                                                                                                                            
	run;                                                                                                                                                                                                                                                           
	%if &syserr.>4 %then %do;                                                                                                                                                                                                                                      
		%Put +--[&sysmacroname: ERROR] -------------------------------;                                                                                                                                                                                               
		%Put | &syserrortext.                                         ;                                                                                                                                                                                               
		%Put +--------------------------------------------------------;                                                                                                                                                                                               
	%end;                                                                                                                                                                                                                                                          
%Mend;                                                                                                                                                                                                                                                          
