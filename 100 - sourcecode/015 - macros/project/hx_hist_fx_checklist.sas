/*
%Let _libout 				= datichk;                                                                                                                                                                                                                                     
%Let _dsOutChecks 	= DWH_COUNTERPARTIES;                                                                                                                                                                                                                        
%Let idAmbito				= A;                                                                                                                                                                                                                                           
%Let dsTarget				= datistg.DWH_COUNTERPARTIES;                                                                                                                                                                                                                  
%Let _dsFxTrace			= datiLkp._ListFunzioni;                                                                                                                                                                                                                      
%Let dsMTD					= prvmeta.DWH_tassonomia_controlli;                                                                                                                                                                                                              
%Let dataProvider		= DWH;                                                                                                                                                                                                                                       
%Let _dsFxCampiTecn = datiLkp._ListCampiTecnici;                                                                                                                                                                                                                
%Let metaFolder = %UnQuote(&projectFolder.)&slash.02_Metadati&slash.&dataProvider.&slash.Tabledata;                                                                                                                                                             
Libname prvmeta	"&metaFolder.";   
*/                                                                                                                                                                                                                              
                                                                                                                                                                                                                                                                
%Macro hx_hist_fx_checklist( ) / store secure ;                                                                                                                                                                                                                 
	%Local _dsCheckDetails _listFieldBusiness _dsDM _dsFx _idFlgRule _dtaSegnFrom                                                                                                                                                                                  
			;                                                                                                                                                                                                                                                            
	Proc Sql;                                                                                                                                                                                                                                                      
			Create Table work.histChecks Like &histchecks.;                                                                                                                                                                                                              
	Quit;                                                                                                                                                                                                                                                          
	%Let _dsCheckDetails = &_libout..&_dsOutChecks.;                                                                                                                                                                                                               
	%If %sysfunc(exist(&_dsCheckDetails.))=0 %Then %Do;                                                                                                                                                                                                            
	  %log(level = E, msg = &_dsCheckDetails. not exist!!);                                                                                                                                                                                                        
		%Goto uscita;                                                                                                                                                                                                                                                 
	%End;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
	%If %sysfunc(exist(&dsTarget.))=0 %Then %Do;                                                                                                                                                                                                                   
	  %log(level = E, msg = &dsTarget. not exist !!);                                                                                                                                                                                                              
		%Goto uscita;                                                                                                                                                                                                                                                 
	%End;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
	%Let _listFieldBusiness =;                                                                                                                                                                                                                                     
	Proc Contents data=&dsTarget. out=_staging_model (Keep=NAME) noprint;                                                                                                                                                                                          
	Run;                                                                                                                                                                                                                                                           
	                                                                                                                                                                                                                                                               
	%Let _FLG_HISTORY = GO;                                                                                                                                                                                                                                        
	%Let _dsFx        = %sysfunc(open(&_dsFxTrace. (Where=(flgExecuteFx='Y'))));                                                                                                                                                                                   
	%Let _dtaSegnFrom = %sysfunc(datetime());                                                                                                                                                                                                                      
	Data work.&_dsOutChecks. (Keep=idRecordStg idRecord                                                                                                                                                                                                            
		%Let _dsDM = %sysfunc(open(_staging_model));                                                                                                                                                                                                                  
		%Do %While(%sysfunc(fetch(&_dsdm.))=0);                                                                                                                                                                                                                       
			%Let _stgName = %sysfunc(getvarc(&_dsDM.,%sysfunc(varnum(&_dsDM.,NAME))));                                                                                                                                                                                   
			%UnQuote(&_stgName.)                                                                                                                                                                                                                                         
		%End;                                                                                                                                                                                                                                                         
		%Let _dsDm = %sysfunc(close(&_dsDM.));                                                                                                                                                                                                                        
			flgName flgViolation dtaSegnFrom dtaSegnTo nrecPerimeter nOccurs                                                                                                                                                                                             
				);                                                                                                                                                                                                                                                          
		;                                                                                                                                                                                                                                                             
		Attrib idRecord			Length=8		label="Id record check" 				format=commax12.                                                                                                                                                                                      
					 idRecordStg	Length=8  	label="Id record staging area" 	format=commax12. 	                                                                                                                                                                                 
					 idAmbito			Length=$15	label="ID Ambito"								format=$15.                                                                                                                                                                                                
					 ;                                                                                                                                                                                                                                                         
		Set &_dsCheckDetails. (Rename=(idRecord=idRecordStg)) end=fine ;                                                                                                                                                                                              
                                                                                                                                                                                                                                                                
		*--Attrib idAmbito 		Length=$6;                                                                                                                                                                                                                               
		Attrib flgName 			Length=$40	label="Flag idRule"                                                                                                                                                                                                              
					 flgViolation	Length=$1		label="Violato Y/N"                                                                                                                                                                                                               
					 dtaSegnFrom  Length=8		label="Data apertura segnalazione"	format=datetime16.                                                                                                                                                                              
					 dtaSegnTo    Length=8		label="Data chiusura segnalazione" 	format=datetime16.                                                                                                                                                                             
				;                                                                                                                                                                                                                                                           
		Retain idRecord . idAmbito "%UnQuote(&idAmbito.)" dtaSegnFrom &_dtaSegnFrom. dtaSegnTo "31dec2999:0:0:0"dt                                                                                                                                                    
			;                                                                                                                                                                                                                                                            
	 	                                                                                                                                                                                                                                                             
		%Do %While (%sysfunc(fetch(&_dsFx.))=0);                                                                                                                                                                                                                      
			%Let _idFlgRule 	 = %sysfunc(getvarc(&_dsFx.,%sysfunc(varnum(&_dsFx.,IdRule))));                                                                                                                                                                             
			%Let _FxName    	 = %sysfunc(getvarc(&_dsFx.,%sysfunc(varnum(&_dsFx.,FxName))));	                                                                                                                                                                            
			%Let _perimeter 	 = %sysfunc(getvarc(&_dsFx.,%sysfunc(varnum(&_dsFx.,perimeter))));	                                                                                                                                                                         
			%Let _flgExecuteFx = %sysfunc(getvarc(&_dsFx.,%sysfunc(varnum(&_dsFx.,flgExecuteFx))));	                                                                                                                                                                     
			%Let _PerformNote  = %sysfunc(getvarc(&_dsFx.,%sysfunc(varnum(&_dsFx.,PerformNote))));	                                                                                                                                                                      
			                                                                                                                                                                                                                                                             
			                                                                                                                                                                                                                                                             
			%Let _flgName   = %NrQuote(flgidRule_%replace_spec_chars(&_idFlgRule., char=_));                                                                                                                                                                             
			%Let _inPerim   = %NrQuote(inperim_%replace_spec_chars(&_idFlgRule., char=_));                                                                                                                                                                               
			                                                                                                                                                                                                                                                             
			%*-- Lookup for key;                                                                                                                                                                                                                                         
			Perimetro_di_applicabilita = "&_perimeter.";                                                                                                                                                                                                                 
			fx_name   								 = "&_fxName.";                                                                                                                                                                                                                            
			eseguito  	 							 = "&_flgExecuteFx.";                                                                                                                                                                                                                     
 			idRule 	  	 							 = "&_idFlgRule.";                                                                                                                                                                                                                       
			flgName   	 							 = "&_flgName.";                                                                                                                                                                                                                          
			flgViolation 							 = &_flgName.;                                                                                                                                                                                                                           
			nota		  								 = "&_PerformNote.";                                                                                                                                                                                                                         
			nrecPerimeter = ifn(&_inPerim.,1,0);                                                                                                                                                                                                                         
			nOccurs				= ifn(&_flgName.='Y',1,0);                                                                                                                                                                                                                        
			If flgViolation='Y' Then Do;                                                                                                                                                                                                                                 
				idRecord = sum(idRecord,1);                                                                                                                                                                                                                                 
				Output;                                                                                                                                                                                                                                                     
			End;                                                                                                                                                                                                                                                         
		%End;                                                                                                                                                                                                                                                         
	Run;                                                                                                                                                                                                                                                           
	%Let _dsFx = %sysfunc(close(&_dsFx));                                                                                                                                                                                                                          
	%Uscita:                                                                                                                                                                                                                                                       
%Mend;                                                                                                                                                                                                                                                          
/*                                                                                                                                                                                                                                                              
Options mprint;                                                                                                                                                                                                                                                 
%hx_hist_fx_checklist();                                                                                                                                                                                                                                        
*/                                                                                                                                                                                                                                                              
