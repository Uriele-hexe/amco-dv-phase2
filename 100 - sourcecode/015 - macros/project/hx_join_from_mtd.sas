*==================================================================================================                                                                                                                                                             
* Cliente : AMCO SpA                                                                                                                                                                                                                                            
* Office  : CRO Portfolio Managment -> Data Verification                                                                                                                                                                                                        
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* Macro name  : hx_join_from_mtd                                                                                                                                                                                                                                
* Description : Macro extracts from metadata single lookup rule                                                                                                                                                                                                 
* Author      : Riccardo Monti Hexe SpA, 23 November 2020                                                                                                                                                                                                       
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* Parameter:                                                                                                                                                                                                                                                    
*    1) dsMtd     : Metadata name                                                                                                                                                                                                                               
*    2) idLookup  : Id lookup                                                                                                                                                                                                                                   
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* Output                                                                                                                                                                                                                                                        
*   The macro updates the data set named "process trace" and produces a log containing the list of                                                                                                                                                              
*	  id lookups performed                                                                                                                                                                                                                                        
*		Data model of last dataset                                                                                                                                                                                                                                   
*				- idTransaction                                                                                                                                                                                                                                            
*				- timestamp                                                                                                                                                                                                                                                
*				- data provider                                                                                                                                                                                                                                            
*				- Code Istitute                                                                                                                                                                                                                                            
*				- Reference date                                                                                                                                                                                                                                           
*				- idLookup performed                                                                                                                                                                                                                                       
*       - Time duration                                                                                                                                                                                                                                         
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* NOTE:                                                                                                                                                                                                                                                         
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* UPDATE HISTORY:                                                                                                                                                                                                                                               
*	  Code				: CHECKS                                                                                                                                                                                                                                            
*		Description : Preliminary checks on (existence on source and lookup table)                                                                                                                                                                                   
*									Preliminary checks on existence on data value to be returned                                                                                                                                                                                          
*		Author      : Uriele De Piano Hexe S.p.A 24 November 2020                                                                                                                                                                                                    
*	.................................................................................................                                                                                                                                                             
*==================================================================================================                                                                                                                                                             
;                                                                                                                                                                                                                                                               
%macro hx_join_from_mtd(dsMtd, idLookup) / store secure minoperator mindelimiter=',';                                                                                                                                                                           
                                                                                                                                                                                                                                                                
    %local                                                                                                                                                                                                                                                      
        _dsid _rc _dsout _dsinCols _dsoutCols _dsin _dsinWhere                                                                                                                                                                                                  
        _dsinKeys _joinType _dsLkp _dsLkpWhere _dsLkpKeys _idLookup                                                                                                                                                                                             
        _joinMatchList _statusChecks _timeStamp                                                                                                                                                                                                                 
    ;                                                                                                                                                                                                                                                           
                                                                                                                                                                                                                                                                
		%Let _timeStamp = %sysfunc(datetime());                                                                                                                                                                                                                       
    %let _joinMatchList = LEFT JOIN, RIGHT JOIN, INNER JOIN, OUTER JOIN, FULL JOIN, HASH;                                                                                                                                                                       
		%*-- UDP;                                                                                                                                                                                                                                                     
    %* Controllo inserimento parametri di input;                                                                                                                                                                                                                
    %if %is_empty(%bquote(&dsMtd.)) %then %do;                                                                                                                                                                                                                  
        %log(level = E, msg = Parameter dsMtd is empty.);                                                                                                                                                                                                       
        %return;                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
    %if %is_empty(%bquote(&idLookup.)) %then %do;                                                                                                                                                                                                               
        %log(level = E, msg = Parameter idLookup is empty.);                                                                                                                                                                                                    
        %return;                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
                                                                                                                                                                                                                                                                
		%*-- UDP: Check existence dsMTD;                                                                                                                                                                                                                              
		%If %sysfunc(exist(&dsMtd.))=0 %Then %Do;                                                                                                                                                                                                                     
        %log(level = E, msg = &dsMTD. not exists);                                                                                                                                                                                                              
        %return;                                                                                                                                                                                                                                                
		%End;                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
    %* Apro tabella metadati + controllo operazione;                                                                                                                                                                                                            
    %let _dsid = %sysfunc(open(&dsMtd. (where=( upcase(id_Lookup) = upcase("&idLookup.") ))));                                                                                                                                                                  
    %if %sysevalf(&_dsid. = 0, boolean) %then %do;                                                                                                                                                                                                              
        %log(level = E, msg = Cannot open dset &dsMtd. with statement: %nrstr(%let _dsid = %sysfunc(open(&dsMtd. (where=( upcase(id_Lookup) = upcase("&idLookup.") ))));));                                                                                     
		%let _dsid = %sysfunc(close(&_dsid.));                                                                                                                                                                                                                        
        %return;                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
		                                                                                                                                                                                                                                                              
    %* Leggo primo record (mi aspetto un solo record per idLookup) + controllo operazione;                                                                                                                                                                      
    %let _rc = %sysfunc(fetch(&_dsid.));                                                                                                                                                                                                                        
    %if %sysevalf(&_rc. ne 0, boolean) %then %do;                                                                                                                                                                                                               
        %log(level = E, msg = Cannot read dset &dsMtd. with statement: %nrstr(%let _rc = %sysfunc(fetch(&_dsid.));));                                                                                                                                           
        %let _dsid = %sysfunc(close(&_dsid.));                                                                                                                                                                                                                  
        %return;                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
                                                                                                                                                                                                                                                                
    %* Estraggo tutti i campi che mi servono dalla tabella dei metadati;                                                                                                                                                                                        
    %let _dsout = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid.,Target_Out))));                                                                                                                                                                             
    %let _dsinCols = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., Source_Out_Values))));                                                                                                                                                                  
    %let _dsoutCols = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., Target_Out_Values))));                                                                                                                                                                 
    %let _dsin = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., Source_Table))));                                                                                                                                                                           
    %let _dsinWhere = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., Source_Where_Clause))));                                                                                                                                                               
    %let _dsinKeys = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., Source_Key))));                                                                                                                                                                         
    %let _joinType = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., Lookup_Method))));                                                                                                                                                                      
    %let _dsLkp = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., Target_Table))));                                                                                                                                                                          
    %let _dsLkpWhere = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid.,Target_Where_Clause))));                                                                                                                                                               
    %let _dsLkpKeys = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., Target_Key))));                                                                                                                                                                        
                                                                                                                                                                                                                                                                
    %* Chiudo tabella metadati;                                                                                                                                                                                                                                 
    %let _dsid = %sysfunc(close(&_dsid.));                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
    %* Controlli sui campi che devono essere obbligatoriamente valorizzati;                                                                                                                                                                                     
    %if not(%qupcase(&_joinType.) in &_joinMatchList.) %then %do;                                                                                                                                                                                               
        %log(level = E, msg = Variable Lookup_Method in MTD table &dsMtd is NOT acceptable.);                                                                                                                                                                   
        %return;                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
    %if %is_empty(%bquote(&_dsout.)) %then %do;                                                                                                                                                                                                                 
        %log(level = E, msg = Variable Target_Out in MTD table &dsMtd is empty.);                                                                                                                                                                               
        %return;                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
    %if %qupcase(&_joinType.) ne HASH and %is_empty(%bquote(&_dsinCols.)) %then %do;                                                                                                                                                                            
        %log(level = E, msg = Variable Source_Out_Values in MTD table &dsMtd is empty.);                                                                                                                                                                        
        %return;                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
		/*                                                                                                                                                                                                                                                            
    %if %qupcase(&_joinType.) ne HASH and %is_empty(%bquote(&_dsoutCols.)) %then %do;                                                                                                                                                                           
        %log(level = E, msg = Variable Target_Out_Values in MTD table &dsMtd is empty.);                                                                                                                                                                        
        %return;                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
		*/                                                                                                                                                                                                                                                            
    %if %is_empty(%bquote(&_dsin.)) %then %do;                                                                                                                                                                                                                  
        %log(level = E, msg = Variable Source_Table in MTD table &dsMtd is empty.);                                                                                                                                                                             
        %return;                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
    %if %is_empty(%bquote(&_dsinKeys.)) %then %do;                                                                                                                                                                                                              
        %log(level = E, msg = Variable Source_Key in MTD table &dsMtd is empty.);                                                                                                                                                                               
        %return;                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
    %if %is_empty(%bquote(&_dsLkp.)) %then %do;                                                                                                                                                                                                                 
        %log(level = E, msg = Variable Target_Table in MTD table &dsMtd is empty.);                                                                                                                                                                             
        %return;                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
    %if %is_empty(%bquote(&_dsLkpKeys.)) %then %do;                                                                                                                                                                                                             
        %log(level = E, msg = Variable Target_Key in MTD table &dsMtd is empty.);                                                                                                                                                                               
        %return;                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
                                                                                                                                                                                                                                                                
    %* Normalizzazione where;                                                                                                                                                                                                                                   
  	%if %bquote(&_dsinWhere.)= %then %let _dsinWhere = 1;                                                                                                                                                                                                        
	  %if %bquote(&_dsLkpWhere.)= %then %let _dsLkpWhere = 1;                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
		*+--[Code:CHECKS]---+                                                                                                                                                                                                                                         
		*| Start updates		|                                                                                                                                                                                                                                           
		*+------------------+;                                                                                                                                                                                                                                        
		%Let _idLookup = %replace_spec_chars(&idLookup., char=_);                                                                                                                                                                                                     
		%Let _statusChecks = LOOKUP_STOP;                                                                                                                                                                                                                             
  	Data _null_;                                                                                                                                                                                                                                                 
			Attrib timeStamp 		Length=8			label="Timestamp" format=Datetime.                                                                                                                                                                                             
						 idRule				Length=$20		label="Id lookup"                                                                                                                                                                                                                  
						 sourceTable	Length=$2000	label="Name of input dataset"                                                                                                                                                                                                   
						 wclsSource		Length=$500		label="Where clause to apply on dataset input"                                                                                                                                                                                  
						 targetTable	Length=$5000	label="Lookup table"                                                                                                                                                                                                            
						 wclsTarget		Length=$500		label="Where clause on lookup table"                                                                                                                                                                                            
						 statusCode		Length=$20		label="Status &sysmacroname. macro"                                                                                                                                                                                              
					;                                                                                                                                                                                                                                                          
			Length sourceKeys targetKeys targetData $1000 fieldList tableField $1000 colName $40                                                                                                                                                                         
					;                                                                                                                                                                                                                                                          
			Retain timeStamp &_timeStamp. idLookup "&_idLookup."                                                                                                                                                                                                         
				;			                                                                                                                                                                                                                                                        
			%*-- Declare hash table regarding process trace;                                                                                                                                                                                                             
			%hx_declare_ht_pr(htTable=&processTrace.);                                                                                                                                                                                                                   
			sourceCode    = symget("sysmacroname");                                                                                                                                                                                                                      
			stepCode      = catx(' ',"Esecuzione della lookup:",idLookup);                                                                                                                                                                                               
			Call Missing(rcCode,msgCode);	                                                                                                                                                                                                                               
			%*-- Get information about source table;                                                                                                                                                                                                                     
			sourceTable = strip(symget("_dsin"));                                                                                                                                                                                                                        
			sourceKeys  = strip(symget("_dsinKeys"));                                                                                                                                                                                                                    
			wclsSource  = Strip(Symget("_dsinWhere"));                                                                                                                                                                                                                   
			sourceData  = Strip(Symget("_dsinCols"));                                                                                                                                                                                                                    
                                                                                                                                                                                                                                                                
			%*-- Get information about lookup table;                                                                                                                                                                                                                     
			targetTable = strip(symget("_dsLkp"));                                                                                                                                                                                                                       
			targetKeys  = strip(symget("_dsLkpKeys"));                                                                                                                                                                                                                   
			wclsTarget  = Strip(Symget("_dsLkpWhere"));                                                                                                                                                                                                                  
			targetData  = Strip(Symget("_dsoutCols"));                                                                                                                                                                                                                   
                                                                                                                                                                                                                                                                
			*-- Check Existence of source and target table;                                                                                                                                                                                                              
			Call Missing(statuDetail);                                                                                                                                                                                                                                   
			If not exist(sourceTable) or not exist(targetTable) Then Do;                                                                                                                                                                                                 
				statusCode = "LOOKUP_STOP";                                                                                                                                                                                                                                 
				rcCode  = -255;                                                                                                                                                                                                                                             
				msgCode = ifc(not exist(sourceTable),catx(' ',sourceTable,"not exists!!")                                                                                                                                                                                   
																								 ,catx(' ',targetTable,"not exists!!")                                                                                                                                                                                                  
														);                                                                                                                                                                                                                                                
				Link esci;                                                                                                                                                                                                                                                  
			End;                                                                                                                                                                                                                                                         
			*-- Check field on source table;                                                                                                                                                                                                                             
			tableField = sourceTable;                                                                                                                                                                                                                                    
			fieldList  = Strip(sourceKeys);                                                                                                                                                                                                                              
			Link checkColumn;                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                                
			*-- Check keys field on lookup table;                                                                                                                                                                                                                        
			tableField = targetTable;                                                                                                                                                                                                                                    
			fieldList  = Strip(targetKeys);                                                                                                                                                                                                                              
			Link checkColumn;	                                                                                                                                                                                                                                           
                                                                                                                                                                                                                                                                
			*-- Check existence of fields to be retrevied from lookup table;                                                                                                                                                                                             
			If Upcase(targetData) not in ("_NULL_") and not missing(targetData) Then Do;                                                                                                                                                                                 
				tableField = targetTable;                                                                                                                                                                                                                                   
				fieldList  = Strip(targetData);                                                                                                                                                                                                                             
				Link checkColumn;                                                                                                                                                                                                                                           
			End;                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
			*-- Check existence of fields to be retrevied from lookup table;                                                                                                                                                                                             
			If Upcase(sourceData) not in ("*") and not missing(sourceData) Then Do;                                                                                                                                                                                      
				tableField = sourceTable;                                                                                                                                                                                                                                   
				fieldList  = Strip(sourceData);                                                                                                                                                                                                                             
				Link checkColumn;                                                                                                                                                                                                                                           
			End;                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
			*-- Check existence returned by lookup data  in source table;                                                                                                                                                                                                
			flgFieldExist = 'N';                                                                                                                                                                                                                                         
			dsSource      = open(sourceTable);                                                                                                                                                                                                                           
			nfields       = Sum(count(Strip(targetData),'-'),1);                                                                                                                                                                                                         
			v             = 1;                                                                                                                                                                                                                                           
			Do While (flgFieldExist='N' And v<=nfields);                                                                                                                                                                                                                 
				colName = strip(scan(Strip(targetData),v,'-'));                                                                                                                                                                                                             
				flgFieldExist = ifc(varnum(dsSource,colName)<=0,'N','Y');                                                                                                                                                                                                   
				v = sum(v,1);                                                                                                                                                                                                                                               
			End;                                                                                                                                                                                                                                                         
			dsSource = close(dsSource);                                                                                                                                                                                                                                  
                                                                                                                                                                                                                                                                
			If flgFieldExist='Y' Then Do;                                                                                                                                                                                                                                
				statusCode = "LOOKUP_STOP";                                                                                                                                                                                                                                 
				msgCode    = catx(' ',colName,"already exists in",sourceTable,"lookup stopped !!");                                                                                                                                                                         
				rcCode     = -253;                                                                                                                                                                                                                                          
				Link esci;                                                                                                                                                                                                                                                  
			End;                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
			rcCode     = 1;                                                                                                                                                                                                                                              
			msgCode    = "Preliminary checks passed";                                                                                                                                                                                                                    
			statusCode = "LOOKUP_GO";                                                                                                                                                                                                                                    
			Link esci;                                                                                                                                                                                                                                                   
			Return;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
			CHECKCOLUMN:                                                                                                                                                                                                                                                 
				dsid          = Open(tableField);                                                                                                                                                                                                                           
				nfields       = Sum(count(fieldList,'-'),1);                                                                                                                                                                                                                
				flgFieldExist = 'Y';                                                                                                                                                                                                                                        
				v             = 1;                                                                                                                                                                                                                                          
				Do While (flgFieldExist='Y' And v<=nfields);                                                                                                                                                                                                                
					colName = strip(scan(fieldList,v,'-'));                                                                                                                                                                                                                    
					flgFieldExist = ifc(varnum(dsid,colName)<=0,'N','Y');                                                                                                                                                                                                      
					v = sum(v,1);                                                                                                                                                                                                                                              
				End;                                                                                                                                                                                                                                                        
				dsid = close(dsid);                                                                                                                                                                                                                                         
				If flgFieldExist='N' Then Do;                                                                                                                                                                                                                               
					statusCode = "LOOKUP_STOP";                                                                                                                                                                                                                                
					msgCode    = catx(' ',colName,"on",tableField,"not exist!!");                                                                                                                                                                                              
					rcCode     = -254;                                                                                                                                                                                                                                         
					Link esci;                                                                                                                                                                                                                                                 
				End;                                                                                                                                                                                                                                                        
			RETURN;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
			ESCI:                                                                                                                                                                                                                                                        
				rc = ht.add();                                                                                                                                                                                                                                              
				Call Symput("_statusChecks",statusCode);                                                                                                                                                                                                                    
				rc = ht.output(dataset:"&processTrace.");                                                                                                                                                                                                                   
				Stop;                                                                                                                                                                                                                                                       
			RETURN;                                                                                                                                                                                                                                                      
		Run;                                                                                                                                                                                                                                                          
		%If  &_statusChecks.=LOOKUP_STOP %Then %Do;                                                                                                                                                                                                                   
      %log(level = E, msg = %NrQuote(&idLookup. stopped !! See process trace));                                                                                                                                                                                 
			%Return;                                                                                                                                                                                                                                                     
		%End;                                                                                                                                                                                                                                                         
		*+--[Code:CHECKS]---+                                                                                                                                                                                                                                         
		*| End updates		|                                                                                                                                                                                                                                             
		*+------------------+;                                                                                                                                                                                                                                        
                                                                                                                                                                                                                                                                
    %if %qupcase(&_joinType.) ne HASH %then %do;                                                                                                                                                                                                                
        %hx_sql_join(                                                                                                                                                                                                                                           
            dsout = &_dsout, dsoutCols = &_dsoutCols,                                                                                                                                                                                                           
            dsin = &_dsin., dsinCols = &_dsinCols., dsinWhere = &_dsinWhere., dsinKeys = &_dsinKeys., joinType = &_joinType.,                                                                                                                                   
            dsLkp = &_dsLkp., dsLkpWhere = &_dsLkpWhere., dsLkpKeys = &_dsLkpKeys.,                                                                                                                                                                             
            idLookup = &idLookup.                                                                                                                                                                                                                               
        );                                                                                                                                                                                                                                                      
    %end;                                                                                                                                                                                                                                                       
    %else %do;                                                                                                                                                                                                                                                  
        %put Chiamato macro HASH;                                                                                                                                                                                                                               
        %hx_lookup_values_hash(                                                                                                                                                                                                                                 
            sourceKeys 	= &_dsin.@&_dsinKeys.                                                                                                                                                                                                                   
            ,sourceKeep = &_dsinCols.                                                                                                                                                                                                                           
            ,sourceWcls = &_dsinWhere.                                                                                                                                                                                                                          
            ,targetKeys = &_dsLkp.@&_dsLkpKeys                                                                                                                                                                                                                  
            ,targetWcls = &_dsLkpWhere.                                                                                                                                                                                                                         
            ,targetKeep = &_dsoutCols.                                                                                                                                                                                                                          
            ,dsOutput 	= &_dsout.                                                                                                                                                                                                                               
            ,idLookup 	= &_idLookup.                                                                                                                                                                                                                            
						,libCode   = work                                                                                                                                                                                                                                         
        			);                                                                                                                                                                                                                                                   
                                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
%mend hx_join_from_mtd;                                                                                                                                                                                                                                         
