*==================================================================================================                                                                                                                                                             
* Cliente : AMCO SpA                                                                                                                                                                                                                                            
* Office  : CRO Portfolio Managment -> Data Verification                                                                                                                                                                                                        
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* Macro name  : hx_lookup_value                                                                                                                                                                                                                                 
* Description : Lookup check                                                                                                                                                                                                                                    
* Author      : Uriele De Piano Hexe SpA, 01 November 2020                                                                                                                                                                                                      
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* NOTE:                                                                                                                                                                                                                                                         
*==================================================================================================                                                                                                                                                             
;                                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                
%Macro hx_lookup_value(lookupFrom 		= staging.HYDRAM_BENI_FULL.cod_garanzia                                                                                                                                                                                     
											,lookupto  			 = %NrQuote(datiwip.GARANZIE_012.cod_garanzia)                                                                                                                                                                                         
											,idRule    			 = XXXX                                                                                                                                                                                                                                
											,retrieveField 	 = _NULL_                                                                                                                                                                                                                            
											,dsDetailsChecks = work.hx_check_lookup_value                                                                                                                                                                                                        
											) / store secure ;                                                                                                                                                                                                                                                   
	%Local checkOnInfo checkLookupInfo sourceTable targetTable sourceCampo targetCampo                                                                                                                                                                             
				 dsDetailsChecks                                                                                                                                                                                                                                            
							;                                                                                                                                                                                                                                                        
                                                                                                                                                                                                                                                                
	%Let checkOnInfo = GO;                                                                                                                                                                                                                                         
	%Let tmpStamp = %sysfunc(putn(%sysfunc(datetime()),datetime.));                                                                                                                                                                                                
	Data _null_;                                                                                                                                                                                                                                                   
		Attrib checkLookupInfo Length=$256                                                                                                                                                                                                                            
			;                                                                                                                                                                                                                                                            
		lookupFrom  = Compress(Symget("lookupFrom"));                                                                                                                                                                                                                 
		lookupto    = Compress(Symget("lookupto"));                                                                                                                                                                                                                   
		sourceTable = catx('.',scan(lookupFrom,1,'.'),scan(lookupFrom,2,'.'));                                                                                                                                                                                        
		sourceCampo = scan(lookupFrom,3,'.');                                                                                                                                                                                                                         
		targetTable = catx('.',scan(lookupto,1,'.'),scan(lookupto,2,'.'));                                                                                                                                                                                            
		targetCampo = scan(lookupto,3,'.');                                                                                                                                                                                                                           
*		Put lookupFrom= lookupto= sourceTable= sourceCampo= targetTable= targetCampo=;                                                                                                                                                                               
		Call Symput("sourceTable",Strip(sourceTable));                                                                                                                                                                                                                
		Call Symput("sourceCampo",Strip(sourceCampo));                                                                                                                                                                                                                
		Call Symput("targetTable",Strip(targetTable));                                                                                                                                                                                                                
		Call Symput("targetCampo",Strip(targetCampo));                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
		Call Missing(checkLookupInfo);                                                                                                                                                                                                                                
		If Not exist(sourceTable) Then                                                                                                                                                                                                                                
			checkLookupInfo = catx(' ',"ERROR:",sourceTable,"not exists!!");                                                                                                                                                                                             
		Else If Not exist(targetTable) Then                                                                                                                                                                                                                           
			checkLookupInfo = catx(' ',"ERROR:",targetTable,"not exists!!");                                                                                                                                                                                             
		Else Do;                                                                                                                                                                                                                                                      
			dsFrom = Open(sourceTable);                                                                                                                                                                                                                                  
			dsTo   = Open(targetTable);                                                                                                                                                                                                                                  
			If varnum(dsFrom,sourceCampo)<=0 Then                                                                                                                                                                                                                        
				checkLookupInfo = catx(' ',"ERROR:",sourceCampo,"not exists in",sourceTable);                                                                                                                                                                               
			Else If varnum(dsTo,targetCampo)<=0 Then                                                                                                                                                                                                                     
				checkLookupInfo = catx(' ',"ERROR:",targetCampo,"not exists in",dsName(dsTo));                                                                                                                                                                              
			Else If Upcase(Symget("retrieveField")) ^= "_NULL_" Then Do;                                                                                                                                                                                                 
				If varnum(dsTo,Symget("retrieveField"))<=0 Then                                                                                                                                                                                                             
					checkLookupInfo = catx(' ',"ERROR:",Symget("retrieveField"),"not exists in",targetTable);                                                                                                                                                                  
			End;                                                                                                                                                                                                                                                         
			dsFrom = Close(dsFrom);                                                                                                                                                                                                                                      
			dsTo	 = Close(dsTo);                                                                                                                                                                                                                                         
		End;                                                                                                                                                                                                                                                          
		If missing(checkLookupInfo) Then checkLookupInfo = "All information about lookup are correct";                                                                                                                                                                
		Else Call Symput("checkOnInfo","STOP");                                                                                                                                                                                                                       
		Call Symput("checkLookupInfo",Strip(checkLookupInfo));                                                                                                                                                                                                        
	Run;                                                                                                                                                                                                                                                           
                                                                                                                                                                                                                                                                
	%Put +---[Macro: &sysmacroname.] ---------------------------------+;                                                                                                                                                                                           
	%Put | Run lookup check										  					 						|;                                                                                                                                                                                                              
	%Put |    1) Source Table : &sourceTable. [&sourceCampo.]						|;                                                                                                                                                                                              
	%Put |    2) Lookup Table : &targetTable. [&targetCampo.]					 	|;                                                                                                                                                                                             
	%Put |    3) Check on info [&checkLookupInfo.]					  					|;                                                                                                                                                                                                   
	%Put |............................................................|;                                                                                                                                                                                           
	%Put | Starting at: &tmpStamp.																		|;                                                                                                                                                                                                             
	%Put +------------------------------------------------------------+;                                                                                                                                                                                           
	%If &checkOnInfo.=STOP %Then %Do;                                                                                                                                                                                                                              
		Data _null_;                                                                                                                                                                                                                                                  
			%*-- Write Process Trace;                                                                                                                                                                                                                                    
			%hx_declare_ht_pr(htTable=&processTrace.);                                                                                                                                                                                                                   
			sourceCode    = symget("sysmacroname");                                                                                                                                                                                                                      
			stepCode      = "Check lookup info [&idRule.]";                                                                                                                                                                                                              
			rcCode        = -41000;                                                                                                                                                                                                                                      
			msgCode       = Strip("&checkLookupInfo.");                                                                                                                                                                                                                  
			rc            = ht.add();                                                                                                                                                                                                                                    
			*rc            = ht.output(dataset:"&processTrace.");                                                                                                                                                                                                        
			rc            = ht.output(dataset:"work._butta");                                                                                                                                                                                                            
		Run;                                                                                                                                                                                                                                                          
		%Goto uscita;                                                                                                                                                                                                                                                 
	%End;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
	%*-- Executes check lookup values;                                                                                                                                                                                                                             
	Data &dsDetailsChecks.;                                                                                                                                                                                                                                        
		Set &sourceTable.                                                                                                                                                                                                                                             
			 	&targetTable. (obs=0 Keep=&targetCampo.                                                                                                                                                                                                                    
			%If %Upcase(&retrieveField.) ne _NULL_ %Then %Do;                                                                                                                                                                                                            
					&retrieveField.                                                                                                                                                                                                                                            
			%End;		                                                                                                                                                                                                                                                      
					);                                                                                                                                                                                                                                                         
		Attrib flgLkp_%UnQuote(&idRule.) Length=$1 Label="Flag lookup rule &idRule.";                                                                                                                                                                                 
		If _N_=1 Then Do;                                                                                                                                                                                                                                             
			Declare hash htLkp(dataset:"&targetTable.",ordered:"yes");                                                                                                                                                                                                   
				htLkp.defineKey("&targetCampo.");                                                                                                                                                                                                                           
			%If %Upcase(&retrieveField.) ne _NULL_ %Then %Do;                                                                                                                                                                                                            
				htLkp.defineData("&retrieveField.");                                                                                                                                                                                                                        
			%End;                                                                                                                                                                                                                                                        
				htLkp.defineDone();                                                                                                                                                                                                                                         
		End;                                                                                                                                                                                                                                                          
		*-- ATTENZIONE: Il flag è impostato a Y se la regola di lookup non è verificata.                                                                                                                                                                            
										In altre parole vale Y se la chiave non è trovata nella tabella di lookup                                                                                                                                                                            
					;                                                                                                                                                                                                                                                          
		%If %Upcase(&retrieveField.) ne _NULL_ %Then %Do;                                                                                                                                                                                                             
			flgLkp_%UnQuote(&idRule.) = ifc(htLkp.check(key:&sourceCampo.)=0,'N','Y');                                                                                                                                                                                   
		%End;                                                                                                                                                                                                                                                         
		%Else %Do;                                                                                                                                                                                                                                                    
			flgLkp_%UnQuote(&idRule.) = ifc(htLkp.find(key:&sourceCampo.)=0,'N','Y');                                                                                                                                                                                    
		%End;                                                                                                                                                                                                                                                         
	Run;                                                                                                                                                                                                                                                           
                                                                                                                                                                                                                                                                
	%Uscita:                                                                                                                                                                                                                                                       
		%Let tmpStamp = %sysfunc(putn(%sysfunc(datetime()),datetime.));                                                                                                                                                                                               
		%Put +---[Macro: &sysmacroname.] ---------------------------------+;                                                                                                                                                                                          
		%Put | Run lookup check										  					 						|;                                                                                                                                                                                                             
		%Put |    1) Source Table : &sourceTable. [&sourceCampo.]						|;                                                                                                                                                                                             
		%Put |    2) Lookup Table : &targetTable. [&targetCampo.]					 	|;                                                                                                                                                                                            
		%Put |    3) Check on info [&checkLookupInfo.]					  					|;                                                                                                                                                                                                  
		%Put |............................................................|;                                                                                                                                                                                          
		%Put | Starting at: &tmpStamp.													;                                                                                                                                                                                                                  
		%Put +-------------------------------------------------+;                                                                                                                                                                                                     
%Mend;                                                                                                                                                                                                                                                          
/*                                                                                                                                                                                                                                                              
%hx_lookup_value(lookupFrom       			= %NrQuote(datistg.HYDRAM_BENI_FULL.cod_garanzia)                                                                                                                                                                          
														 ,lookupto  			= %NrQuote(datiwip.GARANZIE_012.cod_garanzia)                                                                                                                                                                                      
														 ,idRule    		  = XXXX                                                                                                                                                                                                                            
														 ,dsDetailsChecks = work.HYDRAM_BENI_FULL_XXX                                                                                                                                                                                                     
														);                                                                                                                                                                                                                                                
%hx_lookup_value(lookupFrom       			= %NrQuote(datiwip.GARANZIE_012.cod_rapporto)                                                                                                                                                                              
														 ,lookupto  			= %NrQuote(datistg.FIDI_CTPRAPPORTI.cod_rapporto)                                                                                                                                                                                  
														 ,idRule    		  = XXXY                                                                                                                                                                                                                            
														 ,dsDetailsChecks = work.HYDRAM_GARANZIE_012_XXXY                                                                                                                                                                                                 
														);                                                                                                                                                                                                                                                
*/                                                                                                                                                                                                                                                              
