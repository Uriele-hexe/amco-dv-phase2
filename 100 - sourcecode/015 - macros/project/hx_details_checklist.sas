*==================================================================================================                                                                                                                                                             
* Cliente : AMCO SpA                                                                                                                                                                                                                                            
* Office  : CRO Portfolio Managment -> Data Verification                                                                                                                                                                                                        
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* Macro name  : hx_details_checklist                                                                                                                                                                                                                            
* Description : Produce details checsk.                                                                                                                                                                                                                         
* Author      : Uriele De Piano Hexe SpA, 17 January 2021                                                                                                                                                                                                       
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* Parameter:                                                                                                                                                                                                                                                    
*    	1) dsDetail -> Dataset of details name                                                                                                                                                                                                                    
*     In addition at this also list of macro variables used and declared in hx_run_checks_list macro                                                                                                                                                            
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* Output                                                                                                                                                                                                                                                        
*  - Transpose flag check on rows                                                                                                                                                                                                                               
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* NOTE: Macro can be call after hx_run_checks_list                                                                                                                                                                                                              
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
*==================================================================================================                                                                                                                                                             
;                                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                
%Macro hx_details_checklist(dsDetails=_null_ ) / store secure ;                                                                                                                                                                                                 
	%Local _dsCheckDetails _listFieldBusiness _dsDM _dsFx _idFlgRule _dtaSegnFrom _tmpStamp                                                                                                                                                                        
			;                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                                
	%Let _tmpStamp = %sysfunc(putn(%sysfunc(datetime()),datetime.));                                                                                                                                                                                               
	%Put +---[Macro: &sysmacroname.] -------------+;                                                                                                                                                                                                               
	%Put | Produce details check list.						|;                                                                                                                                                                                                                     
	%Put | Dataset	: &dsDetails.									|;                                                                                                                                                                                                                        
	%Put |........................................|;                                                                                                                                                                                                               
	%Put | Started at: &_tmpStamp.;                                                                                                                                                                                                                                
	%Put +----------------------------------------+;                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                
	%Let _dsCheckDetails = &_libout..&_dsOutChecks.;                                                                                                                                                                                                               
	%If %sysfunc(exist(&_dsCheckDetails.))=0 %Then %Do;                                                                                                                                                                                                            
	  %log(level = E, msg = &_dsCheckDetails. not exist!!);                                                                                                                                                                                                        
		%Goto uscita;                                                                                                                                                                                                                                                 
	%End;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
	%If %sysfunc(exist(&dsTarget.))=0 %Then %Do;                                                                                                                                                                                                                   
	  %log(level = E, msg = &dsTarget. not exist !!);                                                                                                                                                                                                              
		%Goto uscita;                                                                                                                                                                                                                                                 
	%End;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
	%Let _listFieldBusiness =;                                                                                                                                                                                                                                     
	Proc Contents data=&dsTarget. out=_staging_model (Keep=NAME) noprint;                                                                                                                                                                                          
	Run;                                                                                                                                                                                                                                                           
	                                                                                                                                                                                                                                                               
	%Let _FLG_HISTORY = GO;                                                                                                                                                                                                                                        
	%Let _dsFx        = %sysfunc(open(&_dsFxTrace. (Where=(flgExecuteFx='Y'))));                                                                                                                                                                                   
	%Let _dtaSegnFrom = %sysfunc(datetime());                                                                                                                                                                                                                      
	Data &dsDetails. (Keep=idRecordStg idRecord                                                                                                                                                                                                                    
		%Let _dsDM = %sysfunc(open(_staging_model));                                                                                                                                                                                                                  
		%Do %While(%sysfunc(fetch(&_dsdm.))=0);                                                                                                                                                                                                                       
			%Let _stgName = %sysfunc(getvarc(&_dsDM.,%sysfunc(varnum(&_dsDM.,NAME))));                                                                                                                                                                                   
			%UnQuote(&_stgName.)                                                                                                                                                                                                                                         
		%End;                                                                                                                                                                                                                                                         
		%Let _dsDm = %sysfunc(close(&_dsDM.));                                                                                                                                                                                                                        
			idRule flagName flagViolation dtaSegnFrom dtaSegnTo nrecPerimeter nOccurs                                                                                                                                                                                    
				);                                                                                                                                                                                                                                                          
		;                                                                                                                                                                                                                                                             
		Attrib idRecord			Length=8		label="Id record check" 				format=commax12.                                                                                                                                                                                      
					 idRecordStg	Length=8  	label="Id record staging area" 	format=commax12. 	                                                                                                                                                                                 
					 idAmbito			Length=$15	label="ID Ambito"								format=$15.                                                                                                                                                                                                
					 ;                                                                                                                                                                                                                                                         
		Set &_dsCheckDetails. (Rename=(idRecord=idRecordStg)) end=fine ;                                                                                                                                                                                              
                                                                                                                                                                                                                                                                
		*--Attrib idAmbito 		Length=$6;                                                                                                                                                                                                                               
		Attrib idRule					Length=$40  label="Id rule"                                                                                                                                                                                                                 
					 flagName 			Length=$40	label="Flag idRule"                                                                                                                                                                                                                
					 flagViolation	Length=$1		label="Violato Y/N"                                                                                                                                                                                                              
					 dtaSegnFrom  	Length=8		label="Data apertura segnalazione"	format=datetime22.                                                                                                                                                                             
					 dtaSegnTo    	Length=8		label="Data chiusura segnalazione" 	format=datetime22.                                                                                                                                                                            
				;                                                                                                                                                                                                                                                           
		Retain idRecord . idAmbito "%UnQuote(&idAmbito.)" dtaSegnFrom &_dtaSegnFrom. dtaSegnTo "31dec2999:0:0:0"dt                                                                                                                                                    
			;                                                                                                                                                                                                                                                            
	 	                                                                                                                                                                                                                                                             
		%Do %While (%sysfunc(fetch(&_dsFx.))=0);                                                                                                                                                                                                                      
			%Let _idFlgRule 	 = %sysfunc(getvarc(&_dsFx.,%sysfunc(varnum(&_dsFx.,IdRule))));                                                                                                                                                                             
			%Let _FxName    	 = %sysfunc(getvarc(&_dsFx.,%sysfunc(varnum(&_dsFx.,FxName))));	                                                                                                                                                                            
			%Let _perimeter 	 = %sysfunc(getvarc(&_dsFx.,%sysfunc(varnum(&_dsFx.,perimeter))));	                                                                                                                                                                         
			%Let _flgExecuteFx = %sysfunc(getvarc(&_dsFx.,%sysfunc(varnum(&_dsFx.,flgExecuteFx))));	                                                                                                                                                                     
			%Let _PerformNote  = %sysfunc(getvarc(&_dsFx.,%sysfunc(varnum(&_dsFx.,PerformNote))));		                                                                                                                                                                     
			                                                                                                                                                                                                                                                             
			%Let _flgName   = %NrQuote(flgidRule_%replace_spec_chars(&_idFlgRule., char=_));                                                                                                                                                                             
			%Let _inPerim   = %NrQuote(inperim_%replace_spec_chars(&_idFlgRule., char=_));                                                                                                                                                                               
			                                                                                                                                                                                                                                                             
			%*-- Lookup for key;                                                                                                                                                                                                                                         
			Perimetro_di_applicabilita = symget("&_perimeter.");                                                                                                                                                                                                         
			fx_name   								 = "&_fxName.";                                                                                                                                                                                                                            
			eseguito  	 							 = "&_flgExecuteFx.";                                                                                                                                                                                                                     
 			idRule 	  	 							 = "&_idFlgRule.";                                                                                                                                                                                                                       
			flagName   	 							 = "&_flgName.";                                                                                                                                                                                                                         
			flagViolation 						 = &_flgName.;                                                                                                                                                                                                                           
			nota		  								 = "&_PerformNote.";                                                                                                                                                                                                                         
			nrecPerimeter 						 = ifn(&_inPerim.,1,0);                                                                                                                                                                                                                  
			nOccurs										 = ifn(&_flgName.='Y',1,0);                                                                                                                                                                                                                 
			If flagViolation='Y' Then Do;                                                                                                                                                                                                                                
				idRecord = sum(idRecord,1);                                                                                                                                                                                                                                 
				Output;                                                                                                                                                                                                                                                     
			End;                                                                                                                                                                                                                                                         
		%End;                                                                                                                                                                                                                                                         
	Run;                                                                                                                                                                                                                                                           
	%Let _dsFx = %sysfunc(close(&_dsFx));                                                                                                                                                                                                                          
	%Uscita:                                                                                                                                                                                                                                                       
		%Let _tmpStamp = %sysfunc(putn(%sysfunc(datetime()),datetime.));                                                                                                                                                                                              
		%Put +---[Macro: &sysmacroname.] -------------+;                                                                                                                                                                                                              
		%Put | Produce details check list.						|;                                                                                                                                                                                                                    
		%Put | Dataset	: &dsDetails.									|;                                                                                                                                                                                                                       
		%Put |........................................|;                                                                                                                                                                                                              
		%Put | Ended at: &_tmpStamp.;                                                                                                                                                                                                                                 
		%Put +----------------------------------------+;                                                                                                                                                                                                              
%Mend;                                                                                                                                                                                                                                                          
*Options Nomprint;                                                                                                                                                                                                                                              
/*                                                                                                                                                                                                                                                              
%Let _libout 				= datichk;                                                                                                                                                                                                                                     
%Let _dsOutChecks 	= DWH_COUNTERPARTIES;                                                                                                                                                                                                                        
%Let idAmbito				= A;                                                                                                                                                                                                                                           
%Let dsTarget				= datistg.DWH_COUNTERPARTIES;                                                                                                                                                                                                                  
%Let _dsFxTrace			= datiLkp._ListFunzioni;                                                                                                                                                                                                                      
%Let dsMTD					= prvmeta.DWH_tassonomia_controlli;                                                                                                                                                                                                              
%Let dataProvider		= DWH;                                                                                                                                                                                                                                       
%Let _dsFxCampiTecn = datiLkp._ListCampiTecnici;                                                                                                                                                                                                                
%Let metaFolder = %UnQuote(&projectFolder.)&slash.02_Metadati&slash.&dataProvider.&slash.Tabledata;                                                                                                                                                             
Libname prvmeta	"&metaFolder.";                                                                                                                                                                                                                                 
%hx_details_checklist(dsDetails=work.&_dsOutChecks.);                                                                                                                                                                                                           
*/                                                                                                                                                                                                                                                              
                                                                                                                                                                                                                                                                
