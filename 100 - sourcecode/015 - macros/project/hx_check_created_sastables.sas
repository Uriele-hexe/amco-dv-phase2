*============================================================================================                                                                                                                                                                   
* Client Name: AMCO SpA                                                                                                                                                                                                                                         
* Project    : Framework di Data Verification                                                                                                                                                                                                                   
*--------------------------------------------------------------------------------------------                                                                                                                                                                   
* Program name: hx_check_created_sastables                                                                                                                                                                                                                      
* Author      : Uriele De Piano Hexe SpA 02 October 2020                                                                                                                                                                                                        
* Description : Check if at least a sastable has been created                                                                                                                                                                                                   
*--------------------------------------------------------------------------------------------                                                                                                                                                                   
* NOTE: Use two global macro variable                                                                                                                                                                                                                           
*============================================================================================                                                                                                                                                                   
;                                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                
%Macro hx_check_created_sastables (libname     = _NULL_                                                                                                                                                                                                         
																	 ,sasCode    = _NULL_                                                                                                                                                                                                                          
																	 ,wclsClause = 1) / store secure;                                                                                                                                                                                                                             
	%Local tmpStamp folderDati                                                                                                                                                                                                                                     
		;	                                                                                                                                                                                                                                                            
	%Let folderDati = %sysfunc(pathName(&libname.));                                                                                                                                                                                                               
	%Let tmpStamp = %sysfunc(datetime());                                                                                                                                                                                                                          
	%Put +- [Macro: &sysmacroname.] --------------------------------------------------;                                                                                                                                                                            
	%Put | Check ho many sas table has been created.																	;                                                                                                                                                                                             
	%Put | Folder 		  : &folderDati. 																							  ;                                                                                                                                                                                                    
	%Put | Where Clause : &wclsClause. 																								;                                                                                                                                                                                                   
	%Put |............................................................................;                                                                                                                                                                            
	%Put | Started at : %sysfunc(putN(&tmpStamp.,datetime.))													;                                                                                                                                                                                         
	%Put +----------------------------------------------------------------------------;                                                                                                                                                                            
                                                                                                                                                                                                                                                                
	Data _null_;                                                                                                                                                                                                                                                   
		Attrib slash 		Length=$1                                                                                                                                                                                                                                      
					 ndataset	Length=8                                                                                                                                                                                                                                         
					 attrFile Length=$256                                                                                                                                                                                                                                      
				;                                                                                                                                                                                                                                                           
		timeStamp  = &tmpStamp.;                                                                                                                                                                                                                                      
		slash      = ifc(symget("sysscp")=:"WIN",'\','/');                                                                                                                                                                                                            
		folderDati = symget("folderDati");                                                                                                                                                                                                                            
		cmdWin = cats('dir /TC "',strip(folderDati)||strip(slash)||'*.sas7bdat"');                                                                                                                                                                                    
		Put cmdWin=;                                                                                                                                                                                                                                                  
		rc  = filename("dlist",cmdWin,"pipe");                                                                                                                                                                                                                        
		fid = fopen("dlist",'s');                                                                                                                                                                                                                                     
		Do While (fread(fid)=0);                                                                                                                                                                                                                                      
			rc = fget(fid,attrFile,256);                                                                                                                                                                                                                                 
			If prxmatch("/sas7bdat/i",attrFile)>0 Then Do;                                                                                                                                                                                                               
				_tmtStampWkb = input(scan(compbl(attrFile),1,' '),anydtdte21.);                                                                                                                                                                                             
				_htm  		 	 = scan(compbl(attrFile),2,' ');                                                                                                                                                                                                                  
				tmtStampWkb  = dhms(_tmtStampWkb,Input(scan(_htm,1,':'),2.),Input(Scan(_htm,2,':'),2.),0);                                                                                                                                                                  
		 		howmanySecond = intck("minute",tmtStampWkb,timeStamp)-(12*60);                                                                                                                                                                                             
				%*-- Attenzione il formato orario è 12 ore e non 24 ore;                                                                                                                                                                                                   
		 		*Put attrFile=;                                                                                                                                                                                                                                            
		 		*Put timeStamp=datetime. tmtStampWkb=datetime. howmanySecond=;                                                                                                                                                                                             
				*-- Check if elapsed time has less of 10 minutes;                                                                                                                                                                                                           
				If int(howmanySecond/60)<=30 Then ndataset = sum(ndataset,1);                                                                                                                                                                                               
			End;                                                                                                                                                                                                                                                         
		End;                                                                                                                                                                                                                                                          
		fid = fclose(fid);                                                                                                                                                                                                                                            
		ndataset = sum(ndataset,0);                                                                                                                                                                                                                                   
		                                                                                                                                                                                                                                                              
		%*-- Write Process Trace;                                                                                                                                                                                                                                     
		%hx_declare_ht_pr;                                                                                                                                                                                                                                            
		sourceCode    = symget("sasCode");                                                                                                                                                                                                                            
		stepCode		  = "Creazione dataset";                                                                                                                                                                                                                            
		If ndataset<=0 Then Do;                                                                                                                                                                                                                                       
			rcCode  = -10000;                                                                                                                                                                                                                                            
			msgCode = cats("Nessun dataset è stato creato nella &libname. [",folderDati,']');                                                                                                                                                                           
		End;                                                                                                                                                                                                                                                          
		Else Do;                                                                                                                                                                                                                                                      
			rcCode  = 1;                                                                                                                                                                                                                                                 
			msgCode = catx(' ',"Sono stati creati",put(ndataset,8.),"dataset nella &libname. [",folderDati,']');                                                                                                                                                         
		End;                                                                                                                                                                                                                                                          
		rc = ht.add();                                                                                                                                                                                                                                                
		rc = ht.output(dataset:"work._processTrace");                                                                                                                                                                                                                 
	Run;                                                                                                                                                                                                                                                           
                                                                                                                                                                                                                                                                
	%If %sysfunc(exist(work._processTrace)) %Then %Do;                                                                                                                                                                                                             
		Proc Sql;                                                                                                                                                                                                                                                     
			Insert Into &processTrace.                                                                                                                                                                                                                                   
				Select *                                                                                                                                                                                                                                                    
				From work._processTrace                                                                                                                                                                                                                                     
				;                                                                                                                                                                                                                                                           
			Drop Table work._processTrace;                                                                                                                                                                                                                               
		Quit;                                                                                                                                                                                                                                                         
	%End;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
	%Let tmpStamp = %sysfunc(datetime());                                                                                                                                                                                                                          
	%Put +- [Macro: &sysmacroname.] --------------------------------------------------;                                                                                                                                                                            
	%Put | Ended at : %sysfunc(putN(&tmpStamp.,datetime.))													;                                                                                                                                                                                           
	%Put +----------------------------------------------------------------------------;                                                                                                                                                                            
%Mend;                                                                                                                                                                                                                                                          
