*==================================================================================================                                                                                                                                                             
* Cliente : AMCO SpA                                                                                                                                                                                                                                            
* Office  : CRO Portfolio Managment -> Data Verification                                                                                                                                                                                                        
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* Macro name  : hx_normalize_datamodel                                                                                                                                                                                                                          
* Description : Macro uses a metadata to link source field with target field                                                                                                                                                                                    
* Author      : Uriele De Piano Hexe SpA, 18 October 2020                                                                                                                                                                                                       
* -------------------------------------------------------------------------------------------------                                                                                                                                                             
* NOTE: Macro is divided in two component                                                                                                                                                                                                                       
*	        1) Retrieve last datiodd dataset created                                                                                                                                                                                                              
*					2) Creation of datiwip table using sas statment inserted in temporary dataset                                                                                                                                                                             
*==================================================================================================                                                                                                                                                             
;                                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                
%Macro hx_normalize_datamodel(dsTableLink = metadata.cmn_data_raccordo                                                                                                                                                                                          
														 ,sourceCode  = _NULL_                                                                                                                                                                                                                            
                             ,dsTarget    = work.hx_target_datamodel                                                                                                                                                                                            
														 ) / store secure;                                                                                                                                                                                                                                               
	%Local timeStamp wlcsDate dsid dsidDP                                                                                                                                                                                                                          
		;                                                                                                                                                                                                                                                             
	%Let timeStamp = %sysfunc(putn(%sysfunc(datetime()),datetime.));                                                                                                                                                                                               
                                                                                                                                                                                                                                                                
	%Put +----[macro=&sysmacroname.] --------------------------------+;                                                                                                                                                                                            
	%Put | Data mapping rename original field vs standard data model |;                                                                                                                                                                                            
	%Put | List of metadata:                                         |;                                                                                                                                                                                            
	%Put |    Source list   : &nplSourceList.												 |;                                                                                                                                                                                                       
	%Put |    Linked fields : &dsTableLink.													 |;                                                                                                                                                                                                        
	%Put | Started at: &timeStamp.																	 |;                                                                                                                                                                                                             
	%Put +-----------------------------------------------------------+;                                                                                                                                                                                            
                                                                                                                                                                                                                                                                
	%*-- Check Metadata Table. Update Process trace;                                                                                                                                                                                                               
	Data _null_;                                                                                                                                                                                                                                                   
		nplSourceList = symget("nplSourceList");                                                                                                                                                                                                                      
		dsTableLink   = symget("dsTableLink");                                                                                                                                                                                                                        
		timeStamp     = datetime();                                                                                                                                                                                                                                   
                                                                                                                                                                                                                                                                
		%hx_declare_ht_pr(htTable=&processTrace.);                                                                                                                                                                                                                    
		                                                                                                                                                                                                                                                              
		sourceCode    = symget("sourceCode");                                                                                                                                                                                                                         
		If Not exist(nplSourceList) Then Do;                                                                                                                                                                                                                          
			stepCode		  = catx(' ',"Verifica Metadata Table",nplSourceList);                                                                                                                                                                                             
			rcCode  = -10010;                                                                                                                                                                                                                                            
			msgCode = "Il Metadata not exists";                                                                                                                                                                                                                          
		End;                                                                                                                                                                                                                                                          
		Else If Not exist(dsTableLink) Then Do;                                                                                                                                                                                                                       
			stepCode = catx(' ',"Verifica Metadata Table",dsTableLink);                                                                                                                                                                                                  
			rcCode   = -10011;                                                                                                                                                                                                                                           
			msgCode = "Il Metadata not exists";                                                                                                                                                                                                                          
		End;                                                                                                                                                                                                                                                          
		Else Do;                                                                                                                                                                                                                                                      
			stepCode = catx(' ',"Verifica dei Metadati [",nplSourceList,"e [",dsTableLink,"]");                                                                                                                                                                          
			msgCode  = "Both existing";                                                                                                                                                                                                                                  
			rcCode   = 1;                                                                                                                                                                                                                                                
		End;                                                                                                                                                                                                                                                          
		rc = ht.add();                                                                                                                                                                                                                                                
		rc = ht.output(dataset:"&processTrace.");                                                                                                                                                                                                                     
		If rcCode<=0 Then                                                                                                                                                                                                                                             
			Abort Abend 0;                                                                                                                                                                                                                                               
	Run;                                                                                                                                                                                                                                                           
                                                                                                                                                                                                                                                                
	%*-- Open source data list, to retrieve list of table loaded;                                                                                                                                                                                                  
	%*-- STEP 1: Retrieve last datiodd dataset created                                                                                                                                                                                                             
		;                                                                                                                                                                                                                                                             
	%hx_list_dataset_created(libname = datiodd,dataList = hx_list_dataset_created);                                                                                                                                                                                
	%*-- STEP 2: Apply algorithm for each dataset;                                                                                                                                                                                                                 
	%Let dsid = %sysfunc(open(hx_list_dataset_created));                                                                                                                                                                                                           
	%Do %While (%sysfunc(Fetch(&dsid.))=0);                                                                                                                                                                                                                        
		%Let Dbcontainer 		  = %Sysfunc(GetvarC(&dsid.,%Sysfunc(Varnum(&dsid.,Dbcontainer))));                                                                                                                                                                        
		%Let tableName	 		  = %Sysfunc(GetvarC(&dsid.,%Sysfunc(Varnum(&dsid.,tableName))));                                                                                                                                                                           
		%Let TableContainer   = %Sysfunc(GetvarC(&dsid.,%Sysfunc(Varnum(&dsid.,TableContainer))));                                                                                                                                                                    
		%Let colTargetValid   = Y;                                                                                                                                                                                                                                    
		%Let colTargetUnValid = ;                                                                                                                                                                                                                                     
		Data _Mapping;                                                                                                                                                                                                                                                
			Set &dsTableLink.                                                                                                                                                                                                                                            
				(Where=(Upcase(Compress(Dbcontainer)) = Compress("%Upcase(&Dbcontainer.)")                                                                                                                                                                                  
					     	And Upcase(Compress(tableName)) = Compress("%Upcase(&TableContainer.)")                                                                                                                                                                              
								%If %symExist(dta_reference) %Then %Do;                                                                                                                                                                                                                 
									And (dtaValid_from<="&dta_reference."d And dtaValid_to>="&dta_reference."d)                                                                                                                                                                            
								%End;                                                                                                                                                                                                                                                   
				))                                                                                                                                                                                                                                                          
					;                                                                                                                                                                                                                                                          
			Attrib colRename Length=$32                                                                                                                                                                                                                                  
					;                                                                                                                                                                                                                                                          
			If flgRaccordo='N' Then Do;                                                                                                                                                                                                                                  
				columnTargetType = columnSourceType;                                                                                                                                                                                                                        
				columnTargetLen  = columnSourceLen;                                                                                                                                                                                                                         
				columnTargetFmt  = columnSourceFmt;                                                                                                                                                                                                                         
			End;                                                                                                                                                                                                                                                         
			If colRuleTransform=:"-- " Then Call Missing(colRuleTransform);                                                                                                                                                                                              
			colRename        = cats("__V_",put(idColumn,Z27.));                                                                                                                                                                                                          
			colRuleTransform = ifc(missing(colRuleTransform),colRename                                                                                                                                                                                                   
                                                            ,prxchange(cats("s/<columnSource>/",colRename,'/'),-1                                                                                                                                               
                                                            ,colRuleTransform)                                                                                                                                                                                  
																											);                                                                                                                                                                                                                                   
			columnTargetFmt = ifc(missing(columnTargetFmt),"_ND_",columnTargetFmt);                                                                                                                                                                                      
			%*-- Check if columnTarget is valid varname;                                                                                                                                                                                                                 
			If Not nvalid(columnTarget,"V7") Then Do;                                                                                                                                                                                                                    
				Call Symput("colTargetValid",'N');                                                                                                                                                                                                                          
				Call Symput("colTargetUnValid",columnTarget);                                                                                                                                                                                                               
				Stop;                                                                                                                                                                                                                                                       
			End;                                                                                                                                                                                                                                                         
		Run;                                                                                                                                                                                                                                                          
		%*-- Abort if at least one column target is unvalid varname;                                                                                                                                                                                                  
		Data _null_;                                                                                                                                                                                                                                                  
			_nUnvalidName = "&colTargetValid.";                                                                                                                                                                                                                          
			%hx_declare_ht_pr(htTable=&processTrace.);		                                                                                                                                                                                                                 
			sourceCode = Symget("sysmacroname");                                                                                                                                                                                                                         
			stepCode	 = "Check valid target column name on &tableName.";                                                                                                                                                                                                 
			rcCode		 = ifn(_nUnvalidName='N',-20000,1);                                                                                                                                                                                                                  
			msgCode 	 = ifc(_nUnvalidName='N',catx(' ',"&colTargetUnValid. nella tabella",Symget("tableName"),"Ã¨ unvalid name")                                                                                                                                         
																		    ,"Tutti i campi target sono valid name"                                                                                                                                                                                                   
																		);                                                                                                                                                                                                                                            
			If _nUnvalidName='N' Then Do;                                                                                                                                                                                                                                
				rc = ht.add();                                                                                                                                                                                                                                              
				rc = ht.output(dataset:"&processTrace.");                                                                                                                                                                                                                   
				Abort Abend 100;                                                                                                                                                                                                                                            
			End;                                                                                                                                                                                                                                                         
		Run;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
		%*-- Normalize;                                                                                                                                                                                                                                               
		%Let dsidDP = %sysfunc(open(_Mapping));                                                                                                                                                                                                                       
		Data datiwip.%UnQuote(&tableName.);                                                                                                                                                                                                                           
			Attrib idTransaction	Length=$25	Label="Id. transaction"                                                                                                                                                                                                      
						 Dbcontainer		Length=$256 Label="Dbcontainer Name"                                                                                                                                                                                                        
						 Tablename			Length=$100 Label="Work sheet or view db table"                                                                                                                                                                                              
						 idRecord				Length=8		Label="Id record"                                                                                                                                                                                                                  
						 ;                                                                                                                                                                                                                                                        
					                                                                                                                                                                                                                                                           
			%Do %While (%sysfunc(Fetch(&dsidDP.))=0);                                                                                                                                                                                                                    
				%Let columnSource 		= %sysfunc(getvarC(&dsidDP.,%sysfunc(varnum(&dsidDP.,columnName))));                                                                                                                                                                    
				%Let columnTarget     = %sysfunc(getvarC(&dsidDP.,%sysfunc(varnum(&dsidDP.,columnTarget))));                                                                                                                                                                
				%Let columnTargetType = %sysfunc(getvarC(&dsidDP.,%sysfunc(varnum(&dsidDP.,columnTargetType))));                                                                                                                                                            
				%Let columnTargetLen  = %sysfunc(getvarN(&dsidDP.,%sysfunc(varnum(&dsidDP.,columnTargetLen))));                                                                                                                                                             
				%Let columnTargetFmt  = %sysfunc(getvarC(&dsidDP.,%sysfunc(varnum(&dsidDP.,columnTargetFmt))));                                                                                                                                                             
				Attrib &columnTarget.                                                                                                                                                                                                                                       
				%If &columnTargetType.=char %Then %Do;                                                                                                                                                                                                                      
					Length=$%UnQuote(&columnTargetLen.)                                                                                                                                                                                                                        
				%End;                                                                                                                                                                                                                                                       
				%Else %Do;                                                                                                                                                                                                                                                  
					Length=8                                                                                                                                                                                                                                                   
				%End;                                                                                                                                                                                                                                                       
				%If &columnTargetFmt. ne _ND_ %Then %Do;                                                                                                                                                                                                                    
					format = &columnTargetFmt.                                                                                                                                                                                                                                 
				%End;                                                                                                                                                                                                                                                       
					label = "&columnSource.";                                                                                                                                                                                                                                  
					;                                                                                                                                                                                                                                                          
			%End;                                                                                                                                                                                                                                                        
		Set datiodd.%UnQuote(&tableName.)                                                                                                                                                                                                                             
			(Rename=(                                                                                                                                                                                                                                                    
			%Let rc = %sysfunc(rewind(&dsidDP.));                                                                                                                                                                                                                        
			%Do %While (%sysfunc(Fetch(&dsidDP.))=0);                                                                                                                                                                                                                    
				%Let columnSource = %sysfunc(getvarC(&dsidDP.,%sysfunc(varnum(&dsidDP.,columnName))));                                                                                                                                                                      
				%Let colRename    = %sysfunc(getvarC(&dsidDP.,%sysfunc(varnum(&dsidDP.,colRename))));                                                                                                                                                                       
				%Let flgValidName = %sysfunc(getvarC(&dsidDP.,%sysfunc(varnum(&dsidDP.,flgValidName))));                                                                                                                                                                    
				%If &flgValidName.=Y %Then %Do;                                                                                                                                                                                                                             
					&columnSource. = &colRename.                                                                                                                                                                                                                               
				%End;                                                                                                                                                                                                                                                       
				%Else %Do;                                                                                                                                                                                                                                                  
					"&columnSource."n = &colRename.                                                                                                                                                                                                                            
				%End;                                                                                                                                                                                                                                                       
			%End;                                                                                                                                                                                                                                                        
			));                                                                                                                                                                                                                                                          
			Drop __V_:                                                                                                                                                                                                                                                   
				;                                                                                                                                                                                                                                                           
			%Let rc = %sysfunc(rewind(&dsidDP.));                                                                                                                                                                                                                        
			%Do %While (%sysfunc(Fetch(&dsidDP.))=0);                                                                                                                                                                                                                    
				%Let columnTarget = %sysfunc(getvarC(&dsidDP.,%sysfunc(varnum(&dsidDP.,columnTarget))));                                                                                                                                                                    
				%Let colRuleTransform    = %sysfunc(getvarC(&dsidDP.,%sysfunc(varnum(&dsidDP.,colRuleTransform))));                                                                                                                                                         
				&columnTarget. = &colRuleTransform.;                                                                                                                                                                                                                        
			%End;                                                                                                                                                                                                                                                        
		Run;                                                                                                                                                                                                                                                          
		%Let dsidDP = %Sysfunc(Close(&dsidDP.));                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
    Data _null_;                                                                                                                                                                                                                                                
			%*-- Write Process Trace;                                                                                                                                                                                                                                    
			Attrib nLinked Length=8                                                                                                                                                                                                                                      
					;                                                                                                                                                                                                                                                          
			dsid  = Open("_Mapping");                                                                                                                                                                                                                                    
			nVars = AttrN(dsid,"NOBS");                                                                                                                                                                                                                                  
			Do While (Fetch(dsid)=0);                                                                                                                                                                                                                                    
				flgRaccordo = GetvarC(dsid,Varnum(dsid,"flgRaccordo"));                                                                                                                                                                                                     
				nLinked = ifn(flgRaccordo='Y',Sum(nLinked,1),nLinked);                                                                                                                                                                                                      
			End;                                                                                                                                                                                                                                                         
			dsid  = Close(dsid);                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
			%hx_declare_ht_pr(htTable=&processTrace.);		                                                                                                                                                                                                                 
			sourceCode = symget("sourceCode");                                                                                                                                                                                                                           
			stepCode	 = catx(' ',"Mapping ",Symget("tableName"),'[',Symget("TableContainer"),"] vs common data model");                                                                                                                                                  
			rcCode		 = 1;                                                                                                                                                                                                                                                
			msgCode    = catx(' ',"Sono stati raccordati"                                                                                                                                                                                                                
                                 ,put(nLinked,12.),"campi su un totale di "                                                                                                                                                                                     
                                 ,put(nVars,12.),"campi");                                                                                                                                                                                                      
			rc = ht.add();                                                                                                                                                                                                                                               
			rc = ht.output(dataset:"&processTrace.");                                                                                                                                                                                                                    
		Run;                                                                                                                                                                                                                                                          
		%*-- Storicizza la tracciatura;                                                                                                                                                                                                                               
		%Let dsMappingHist = datitrc.%UnQuote(&dataProvider.)_MAPPING_HISTORY;                                                                                                                                                                                        
		%If %sysfunc(exist(&dsMappingHist.))=0 %Then %Do;                                                                                                                                                                                                             
			Data &dsMappingHist.;                                                                                                                                                                                                                                        
				Attrib idTransaction Length=$20                                                                                                                                                                                                                             
						;                                                                                                                                                                                                                                                         
				Set _mapping (obs  = 0                                                                                                                                                                                                                                      
                              Keep = Dbcontainer Tablename idColumn columnName flgRaccordo columnTarget:                                                                                                                                                        
                                     colRename colRuleTransform                                                                                                                                                                                                 
                              );                                                                                                                                                                                                                                
			Run;                                                                                                                                                                                                                                                         
		%End;                                                                                                                                                                                                                                                         
		Data &dsMappingHist.;                                                                                                                                                                                                                                         
			Set &dsMappingHist. (in=dahist)                                                                                                                                                                                                                              
					_mapping (in   = daload                                                                                                                                                                                                                                    
                              Keep = Dbcontainer Tablename idColumn columnName flgRaccordo columnTarget:                                                                                                                                                        
                                     colRename colRuleTransform                                                                                                                                                                                                 
                              );                                                                                                                                                                                                                                
			if daload Then idTransaction = symget("idTrasaction");                                                                                                                                                                                                       
		Run;                                                                                                                                                                                                                                                          
	%End;                                                                                                                                                                                                                                                          
	%Let dsid = %Sysfunc(Close(&dsid.));                                                                                                                                                                                                                           
%Mend;                                                                                                                                                                                                                                                          
