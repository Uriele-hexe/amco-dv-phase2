*============================================================================================                                                                                                                                                                   
* Client Name: AMCO SpA                                                                                                                                                                                                                                         
* Project    : Framework di Data Verification                                                                                                                                                                                                                   
*--------------------------------------------------------------------------------------------                                                                                                                                                                   
* Program name: hx_import_ldt                                                                                                                                                                                                                                   
* Author      : Uriele De Piano Hexe SpA 02 October 2020                                                                                                                                                                                                        
* Description : This program uses CMN_DATA_MAPPING (filtered by <data provider>) to retrieve                                                                                                                                                                    
*               list of worksheet to be imported                                                                                                                                                                                                                
*--------------------------------------------------------------------------------------------                                                                                                                                                                   
* NOTE: Use two global macro variable                                                                                                                                                                                                                           
*--------------------------------------------------------------------------------------------                                                                                                                                                                   
* CHANGE HISTORY                                                                                                                                                                                                                                                
*			Code		: FONTEDATI                                                                                                                                                                                                                                           
*			Description	: User field named Fontedati, to check support data (Excel or others support)                                                                                                                                                                   
*			Author		: Uriele De Piano Hexe S.p.A 12 November 2020                                                                                                                                                                                                       
*     -----------------------------------------------------------------------------------------                                                                                                                                                                 
*			Code		: WHERECLAUSE                                                                                                                                                                                                                                         
*			Description	: User field named whereClause, to check filter data (Excel or others support)                                                                                                                                                                  
*			Author		: Uriele De Piano Hexe S.p.A 12 November 2020                                                                                                                                                                                                       
*     -----------------------------------------------------------------------------------------                                                                                                                                                                 
*			Code		: LDT_IMPORT                                                                                                                                                                                                                                          
*			Description	: Raise macro code with the recall a new macro named import_xlsx_sheets                                                                                                                                                                         
*			Author		: Uriele De Piano Hexe S.p.A 12 November 2020                                                                                                                                                                                                       
*============================================================================================                                                                                                                                                                   
;                                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                
%Macro hx_import_ldt(metaWorkbook=METADATA.CMN_DATA_MAPPING                                                                                                                                                                                                     
										,dataProvider=_NULL_,refDate=_NULL_) / store secure;                                                                                                                                                                                                                 
	%local dsWhere tmpStamp dsid                                                                                                                                                                                                                                   
	   ;                                                                                                                                                                                                                                                           
	%Let tmpStamp = %sysfunc(putn(%sysfunc(datetime()),datetime.));                                                                                                                                                                                                
	%Put +-- [Macro: &sysmacroname.] --------------------------------------------------;                                                                                                                                                                           
	%Put | Loan Data Tape from an woorkBook                                            ;                                                                                                                                                                           
    %Put |   Data provider  : &dataProvider. 		                                       ;                                                                                                                                                                         
	%Put |   Reference Date : &refDate.                                                ;                                                                                                                                                                           
	%Put |.............................................................................;                                                                                                                                                                           
	%Put | Started at: &tmpStamp.;                                                                                                                                                                                                                                 
	%Put +-----------------------------------------------------------------------------;                                                                                                                                                                           
                                                                                                                                                                                                                                                                
	*-- Create a where;                                                                                                                                                                                                                                            
	%if "%Upcase(&dataProvider.)" = "_NULL_" %then %Do;                                                                                                                                                                                                            
	  %Put +-- [&sysmacroname.: ERROR] --------------------------------------------------;                                                                                                                                                                         
	  %Put | Missing value regarding dataProvider parameter                              ;                                                                                                                                                                         
	  %Put | Import will be stopped     		                                       ;                                                                                                                                                                                 
	  %Put +-----------------------------------------------------------------------------;                                                                                                                                                                         
	  %Return;                                                                                                                                                                                                                                                     
	%End;                                                                                                                                                                                                                                                          
  *-- Compose where clause to apply on metadata npl_source_list;                                                                                                                                                                                                
	%Let dsWhere=1;                                                                                                                                                                                                                                                
	Data _null_;                                                                                                                                                                                                                                                   
		Attrib dsWhere Length=$300                                                                                                                                                                                                                                    
		    ;                                                                                                                                                                                                                                                         
		dsWhere = cats("Upcase(dataProvider)='",Upcase(symget("dataProvider")),"'");                                                                                                                                                                                  
		refDate = Upcase(symget("refDate"));                                                                                                                                                                                                                          
		If strip(refDate) ^= "_NULL_" then do;                                                                                                                                                                                                                        
		  dtaRefDate = put(input(refDate,date9.),12.);                                                                                                                                                                                                                
			dsWhere = catx(' ',dsWhere,"and (dtaValid_from<=",dtaRefDate,"and dtaValid_to>=",dtaRefDate,')');                                                                                                                                                            
		end;                                                                                                                                                                                                                                                          
		Call Symput("dsWhere",dsWhere);                                                                                                                                                                                                                               
	Run;                                                                                                                                                                                                                                                           
	%Put &=dsWhere;                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
	%*--[Code: LDT_IMPORT] ----------------------;                                                                                                                                                                                                                 
    %If &dataProvider.=LDT %Then %Do;                                                                                                                                                                                                                           
	  %import_xlsx_sheets(%bquote(&ldt_fullName.), work, whr_clause=1);                                                                                                                                                                                            
	%End;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
	*-- Open metada table;                                                                                                                                                                                                                                         
	%if %sysfunc(exist(&metaWorkbook.))=0 %Then %Do;                                                                                                                                                                                                               
		%Put +-- [&sysmacroname.: ERROR] --------------------------------------------------;                                                                                                                                                                          
		%Put | Table named &metaWorkbook. not exists                                   ;                                                                                                                                                                              
	    %Put | Import will be stopped     		                                       ;                                                                                                                                                                               
		%Put +-----------------------------------------------------------------------------;                                                                                                                                                                          
		%Return;                                                                                                                                                                                                                                                      
	%End;                                                                                                                                                                                                                                                          
	%Let idSheet = 0;                                                                                                                                                                                                                                              
	%Let dsid = %sysfunc(open(&metaWorkbook. (Where=(&dsWhere.))));                                                                                                                                                                                                
	%Do %While(%sysfunc(fetch(&dsid.))=0);                                                                                                                                                                                                                         
		%let wkbFolder   = %sysfunc(getvarc(&dsid.,%sysfunc(varnum(&dsid.,wkbFolder))));                                                                                                                                                                              
		%let dbContainer = %sysfunc(getvarc(&dsid.,%sysfunc(varnum(&dsid.,dbContainer))));                                                                                                                                                                            
		%let tableName   = %sysfunc(getvarc(&dsid.,%sysfunc(varnum(&dsid.,tableName))));                                                                                                                                                                              
		%Let tableAlias = %sysfunc(getvarc(&dsid.,%sysfunc(varnum(&dsid.,tableAlias))));                                                                                                                                                                              
                                                                                                                                                                                                                                                                
		%*-- [Change code: FONTEDATI] --------------------------------------------------------;                                                                                                                                                                       
		%Let Fontedati  = %sysfunc(getvarc(&dsid.,%sysfunc(varnum(&dsid.,Fontedati))));                                                                                                                                                                               
                                                                                                                                                                                                                                                                
		%*-- [Change code: WHERECLAUSE] --------------------------------------------------------;                                                                                                                                                                     
		%Let whereClause = %sysfunc(getvarc(&dsid.,%sysfunc(varnum(&dsid.,whereClause))));                                                                                                                                                                            
		%*-- Update whereClause;                                                                                                                                                                                                                                      
		Data _null_;                                                                                                                                                                                                                                                  
			whereClause = Strip(Symget("whereClause"));                                                                                                                                                                                                                  
			If missing(whereClause) Then Call Symput("whereClause",'1');                                                                                                                                                                                                 
		Run;                                                                                                                                                                                                                                                          
		%Put +-- [&sysmacroname.: ERROR] --------------------------------------------------;                                                                                                                                                                          
		%Put | &=whereClause;                                                                                                                                                                                                                                         
	  %Put +-----------------------------------------------------------------------------;                                                                                                                                                                         
                                                                                                                                                                                                                                                                
		%let idSheet = %Eval(&idSheet.+1);                                                                                                                                                                                                                            
		Data _null_;                                                                                                                                                                                                                                                  
			Attrib dsimport Length=$28                                                                                                                                                                                                                                   
                   tableAlias Length=$80                                                                                                                                                                                                                        
			    ;                                                                                                                                                                                                                                                        
			tableAlias = Symget("tableAlias");                                                                                                                                                                                                                           
			dsImport = Ifc(not missing(tableAlias)                                                                                                                                                                                                                       
										 ,tableAlias                                                                                                                                                                                                                                          
										 ,compress(translate("&tableName.",' ','$'))                                                                                                                                                                                                          
										 );                                                                                                                                                                                                                                                   
			call symputX("dsimport",catx('.',"datiodd",catx('_',dsimport,put(&idSheet.,Z3.))),'L');                                                                                                                                                                      
		Run;                                                                                                                                                                                                                                                          
		%If %sysfunc(exist(&dsImport.)) %Then %Do;                                                                                                                                                                                                                    
			Proc Delete data=&&dsImport.; Run;                                                                                                                                                                                                                           
		%End;                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
		%If %Upcase(&Fontedati.)=EXCEL %Then %Do;                                                                                                                                                                                                                     
          %*--[Code: LDT_IMPORT] ----------------------;                                                                                                                                                                                                        
		  /*%hx_import_sheet(wkbFolder=&wkbFolder.                                                                                                                                                                                                                    
									    ,wkbName=&dbContainer.                                                                                                                                                                                                                             
										,wkbSheet=%NrQuote(&tableName.)                                                                                                                                                                                                                       
								        ,dsImport=&dsImport.);*/                                                                                                                                                                                                                        
		%End;                                                                                                                                                                                                                                                         
		Data &dsImport.;                                                                                                                                                                                                                                              
			%if %symexist(idTrasaction) %then %do;                                                                                                                                                                                                                       
				Attrib idTransaction Length=$25 label="Id. transanction";                                                                                                                                                                                                   
				Retain idTransaction "&idTrasaction.";                                                                                                                                                                                                                      
			%end;                                                                                                                                                                                                                                                        
				Attrib dbContainer Length=$256                                                                                                                                                                                                                              
                       tableName   Length=$100                                                                                                                                                                                                                  
							 ;                                                                                                                                                                                                                                                       
				Retain dbContainer "&dbContainer." tableName "&tableName.";                                                                                                                                                                                                 
			Attrib idRecord Length=8 label="Id. record";                                                                                                                                                                                                                 
			Set                                                                                                                                                                                                                                                          
			%If %Upcase(&Fontedati.)=EXCEL %Then %Do;                                                                                                                                                                                                                    
               %*--[Code: LDT_IMPORT] ----------------------;                                                                                                                                                                                                   
			   "&tableName."n (Where=(&whereClause.))                                                                                                                                                                                                                    
			%End;                                                                                                                                                                                                                                                        
			%Else %Do;                                                                                                                                                                                                                                                   
				%UnQuote(&dbContainer.).%UnQuote(&tableName.) (Where=(&whereClause.))                                                                                                                                                                                       
			%End;                                                                                                                                                                                                                                                        
			;                                                                                                                                                                                                                                                            
			idRecord = _N_;                                                                                                                                                                                                                                              
		Run;                                                                                                                                                                                                                                                          
	%End;                                                                                                                                                                                                                                                          
	%Let dsid = %sysfunc(close(&dsid.));                                                                                                                                                                                                                           
	%Uscita:                                                                                                                                                                                                                                                       
	  %Let tmpStamp = %sysfunc(putn(%sysfunc(datetime()),datetime.));                                                                                                                                                                                              
	  %Put +-- [Macro: &sysmacroname.] --------------------------------------------------;                                                                                                                                                                         
	  %Put | Loan Data Tape from an woorkBook                                            ;                                                                                                                                                                         
	  %Put |   Data provider  : &dataProvider. 		                                       ;                                                                                                                                                                          
	  %Put |   Reference Date : &refDate.                                                ;                                                                                                                                                                         
	  %Put |.............................................................................;                                                                                                                                                                         
	  %Put | Ended at: &tmpStamp.;                                                                                                                                                                                                                                 
	  %Put +-----------------------------------------------------------------------------;                                                                                                                                                                         
%Mend;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
