/*{                                                                                                                                                                                                                                                             
    "title": "delete_before_append.sas",                                                                                                                                                                                                                        
    "desc": "Macro che effettua il cancella appendi di una tabella di input su una tabella di output in base alla chiave.",                                                                                                                                     
    "params": [                                                                                                                                                                                                                                                 
        { "_input": "Nome del dataset di input da appendere." },                                                                                                                                                                                                
        { "_output": "Nome del dataset di output su cui cancellare per la chiave e appendere." },                                                                                                                                                               
        { "key": "Lista di campi separati dallo spazio che indicano la chiave delle tabelle." },                                                                                                                                                                
		{ "force_append": "Opzione di append. Di default non effettua la force. Indicare force se si vuole forzare l'append in presenza di differenze di struttura." }                                                                                                
    ],                                                                                                                                                                                                                                                          
    "return": "void: Non restituisce alcun valore.",                                                                                                                                                                                                            
    "example": "%delete_before_append(_input = temp, _output = sashelp.class, key=name)",                                                                                                                                                                       
    "author": "Hexe S.p.A.",                                                                                                                                                                                                                                    
    "website": "http://www.hexeitalia.com",                                                                                                                                                                                                                     
	"email": "<info@hexeitalia.com>",                                                                                                                                                                                                                              
	"mantainers": [ "Hexe S.p.A." ]                                                                                                                                                                                                                                
}*/                                                                                                                                                                                                                                                             
                                                                                                                                                                                                                                                                
%macro delete_before_append(_input=, _output=, key=, force_append= ) / store secure ;                                                                                                                                                                           
                                                                                                                                                                                                                                                                
	%local _i _key;                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
	%if %attrn(&_input., nobs)=0 %then %do;                                                                                                                                                                                                                        
		%log(level = W, msg = No record in dataset: %upcase(&_input.). No appending done.);                                                                                                                                                                           
		%return;                                                                                                                                                                                                                                                      
	%end;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
	%if %sysfunc(exist(&_output.))=0 %then %do;                                                                                                                                                                                                                    
		%log(msg = Dataset &_output. does not exist. It gets created for the first time with input table &_input.);                                                                                                                                                   
		data &_output.;                                                                                                                                                                                                                                               
			set &_input.;                                                                                                                                                                                                                                                
		run;                                                                                                                                                                                                                                                          
		%check_rc;                                                                                                                                                                                                                                                    
		%return;                                                                                                                                                                                                                                                      
	%end;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
	proc sort data=&_input.(keep=&key.) nodupkey out=tmp_hash_tab;                                                                                                                                                                                                 
		by &key.;                                                                                                                                                                                                                                                     
	run;                                                                                                                                                                                                                                                           
	%check_rc;                                                                                                                                                                                                                                                     
	                                                                                                                                                                                                                                                               
	%* Creo la lista dei nomi delle chiavi da usare nella hash;                                                                                                                                                                                                    
	%let _i = 1;                                                                                                                                                                                                                                                   
	%let _key = ;                                                                                                                                                                                                                                                  
                                                                                                                                                                                                                                                                
	%do %while (%scan(&key.,&_i," ")^=);                                                                                                                                                                                                                           
		%let testo=%scan(&key.,&_i," ");                                                                                                                                                                                                                              
		%if &_i = 1 %then %do;                                                                                                                                                                                                                                        
			%let _key = "&testo" ;                                                                                                                                                                                                                                       
		%end;                                                                                                                                                                                                                                                         
		%else %do;                                                                                                                                                                                                                                                    
			%let _key = &_key , "&testo" ;                                                                                                                                                                                                                               
		%end;                                                                                                                                                                                                                                                         
		%if %has_variable(&_input., &testo.)=0 %then %do;                                                                                                                                                                                                             
			%log(level = E, msg = Variable &testo. does not exist in dataset &_input.);                                                                                                                                                                                  
			%return;                                                                                                                                                                                                                                                     
		%end;                                                                                                                                                                                                                                                         
		%let _i = %eval(&_i+1);                                                                                                                                                                                                                                       
	%end;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
	%* Cancello tramite hashmap i record nella tabella di output;                                                                                                                                                                                                  
	data &_output.;                                                                                                                                                                                                                                                
		set &_output.;                                                                                                                                                                                                                                                
		drop rc;                                                                                                                                                                                                                                                      
		if _n_=1 Then do;                                                                                                                                                                                                                                             
		declare hash hrese(hashexp:8, dataset:"tmp_hash_tab");                                                                                                                                                                                                        
			hrese.defineKey(&_key);                                                                                                                                                                                                                                      
			hrese.defineDone();                                                                                                                                                                                                                                          
		end;                                                                                                                                                                                                                                                          
		rc = hrese.find();                                                                                                                                                                                                                                            
		if rc=0 then delete;                                                                                                                                                                                                                                          
	run;                                                                                                                                                                                                                                                           
	%check_rc;                                                                                                                                                                                                                                                     
	                                                                                                                                                                                                                                                               
	%* Appendo i nuovi record nella tabella di output;                                                                                                                                                                                                             
	proc append                                                                                                                                                                                                                                                    
		base = &_output.                                                                                                                                                                                                                                              
		data = &_input. &force_append.;                                                                                                                                                                                                                               
	run;                                                                                                                                                                                                                                                           
                                                                                                                                                                                                                                                                
	proc delete data=tmp_hash_tab; run;                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                                
%mend delete_before_append;                                                                                                                                                                                                                                     
