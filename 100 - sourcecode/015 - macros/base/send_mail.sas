%macro send_mail(id_mail, mtd_mail, whr_clause=1 ) / store secure ;                                                                                                                                                                                             
                                                                                                                                                                                                                                                                
    %local _dsid _rc _i _e _whr _from _to __to _cc __cc _ccn __ccn _importance _subject _attach _body;                                                                                                                                                          
                                                                                                                                                                                                                                                                
    %* Controllo che siano stati inseriti i parametri di input;                                                                                                                                                                                                 
    %if %is_empty(%bquote(&mtd_mail.)) %then %do;                                                                                                                                                                                                               
        %log(level = E, msg = %bquote(Parameter mtd_mail is empty.));                                                                                                                                                                                           
        %return;                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
    %if %is_empty(%bquote(&id_mail.)) %then %do;                                                                                                                                                                                                                
        %log(level = E, msg = %bquote(Parameter id_mail is empty.));                                                                                                                                                                                            
        %return;                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
                                                                                                                                                                                                                                                                
    %* Apro il metadato delle mail per recuperare le informazioni sulla mail da inviare;                                                                                                                                                                        
    %let _whr = &whr_clause. and id_mail="&id_mail.";                                                                                                                                                                                                           
    %let _dsid = %sysfunc(open(&mtd_mail. (where=( &_whr. ))));                                                                                                                                                                                                 
    %if &_dsid. <= 0 %then %do;                                                                                                                                                                                                                                 
        %log(level = E, msg = %bquote(Cannot open dataset &mtd_mail. with (where=( &_whr. ))));                                                                                                                                                                 
        %return;                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
    %let _rc = %sysfunc(fetch(&_dsid.));                                                                                                                                                                                                                        
    %if &_rc. ne 0 %then %do;                                                                                                                                                                                                                                   
        %log(level = E, msg = %bquote(Cannot read (fetch) dataset &mtd_mail. with (where=( &_whr. ))));                                                                                                                                                         
        %return;                                                                                                                                                                                                                                                
    %end;                                                                                                                                                                                                                                                       
    %let _from = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., from))));                                                                                                                                                                                   
    %let _to = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., to))));                                                                                                                                                                                       
    %let _i = 1;                                                                                                                                                                                                                                                
    %let _e = %qscan(%bquote(&_to.), &_i., %str(,));                                                                                                                                                                                                            
    %do %while(%is_empty(%bquote(&_e.))=0);                                                                                                                                                                                                                     
        %let __to = &__to. "%sysfunc(strip(&_e.))";                                                                                                                                                                                                             
        %let _i = %eval(&_i.+1);                                                                                                                                                                                                                                
        %let _e = %qscan(%bquote(&_to.), &_i., %str(,));                                                                                                                                                                                                        
    %end;                                                                                                                                                                                                                                                       
    %let _cc = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., cc))));                                                                                                                                                                                       
    %let _i = 1;                                                                                                                                                                                                                                                
    %let _e = %qscan(%bquote(&_cc.), &_i., %str(,));                                                                                                                                                                                                            
    %do %while(%is_empty(%bquote(&_e.))=0);                                                                                                                                                                                                                     
        %let __cc = &__cc. "%sysfunc(strip(&_e.))";                                                                                                                                                                                                             
        %let _i = %eval(&_i.+1);                                                                                                                                                                                                                                
        %let _e = %qscan(%bquote(&_cc.), &_i., %str(,));                                                                                                                                                                                                        
    %end;                                                                                                                                                                                                                                                       
    %let _ccn = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., ccn))));                                                                                                                                                                                     
    %let _i = 1;                                                                                                                                                                                                                                                
    %let _e = %qscan(%bquote(&_ccn.), &_i., %str(,));                                                                                                                                                                                                           
    %do %while(%is_empty(%bquote(&_e.))=0);                                                                                                                                                                                                                     
        %let __ccn = &__ccn. "%sysfunc(strip(&_e.))";                                                                                                                                                                                                           
        %let _i = %eval(&_i.+1);                                                                                                                                                                                                                                
        %let _e = %qscan(%bquote(&_ccn.), &_i., %str(,));                                                                                                                                                                                                       
    %end;                                                                                                                                                                                                                                                       
    %let _importance = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., importance))));                                                                                                                                                                       
    %let _subject = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., subject))));                                                                                                                                                                             
    %let _attach = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., attach))));                                                                                                                                                                               
    %let _body = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., body))));                                                                                                                                                                                   
    %let _dsid = %sysfunc(close(&_dsid.));                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
    %* Invio la mail;                                                                                                                                                                                                                                           
    %log(msg = Sending mail);                                                                                                                                                                                                                                   
    %log(msg = from: %bquote(&_from.));                                                                                                                                                                                                                         
    %log(msg = to: %bquote(&_to.));                                                                                                                                                                                                                             
    %log(msg = cc: %bquote(&_cc.));                                                                                                                                                                                                                             
    %log(msg = ccn: %bquote(&_ccn.));                                                                                                                                                                                                                           
    %log(msg = subject: %bquote(&_subject.));                                                                                                                                                                                                                   
    filename eml email                                                                                                                                                                                                                                          
        type='text/html'                                                                                                                                                                                                                                        
        from = "&_from."                                                                                                                                                                                                                                        
        to = (&__to.)                                                                                                                                                                                                                                           
        %if %is_empty(%bquote(&_cc.))=0 %then %do;                                                                                                                                                                                                              
            cc = (&__cc.)                                                                                                                                                                                                                                       
        %end;                                                                                                                                                                                                                                                   
        %if %is_empty(%bquote(&_ccn.))=0 %then %do;                                                                                                                                                                                                             
            bcc = (&__ccn.)                                                                                                                                                                                                                                     
        %end;                                                                                                                                                                                                                                                   
        %if %is_empty(%bquote(&_importance.))=0 %then %do;                                                                                                                                                                                                      
            importance = "&_importance."                                                                                                                                                                                                                        
        %end;                                                                                                                                                                                                                                                   
        subject = "&_subject."                                                                                                                                                                                                                                  
        %if %is_empty(%bquote(&_attach.))=0 %then %do;                                                                                                                                                                                                          
            attach = "&_attach."                                                                                                                                                                                                                                
        %end;                                                                                                                                                                                                                                                   
    ;                                                                                                                                                                                                                                                           
    data _null_;                                                                                                                                                                                                                                                
        file eml;                                                                                                                                                                                                                                               
        put "&_body.";                                                                                                                                                                                                                                          
    run;                                                                                                                                                                                                                                                        
    filename eml clear;                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
%mend send_mail;                                                                                                                                                                                                                                                
