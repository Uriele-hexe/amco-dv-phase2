/*{                                                                                                                                                                                                                                                             
	"Descrizione": "Macro che setta dinamicamente le libname inserite in un metadato specifico (Colonne: [LIB, PATH, ENGINE, OPTIONS, CREA_PATH]).",                                                                                                               
	"Parametri": [                                                                                                                                                                                                                                                 
		{ "dset": "Nome del dataset metadato contenente le libname da settare." },                                                                                                                                                                                    
		{ "whr_clause": "Clausola di where da applicare in fase di estrazione del metadato di input." }                                                                                                                                                               
	],                                                                                                                                                                                                                                                             
	"Return": "void: non ritorna alcun valore.",                                                                                                                                                                                                                   
	"Esempio": "<code>%set_libs(mtd_libs);",                                                                                                                                                                                                                       
	"Autore": "Hexe S.p.A.",                                                                                                                                                                                                                                       
	"Sito web": "<http://www.hexeitalia.com>",                                                                                                                                                                                                                     
	"Email": "<info@hexeitalia.com>",                                                                                                                                                                                                                              
	"Manutentori": [ "Hexe S.p.A." ]                                                                                                                                                                                                                               
}*/                                                                                                                                                                                                                                                             
                                                                                                                                                                                                                                                                
%macro set_libs(dset, whr_clause=1 ) / store secure ;                                                                                                                                                                                                           
                                                                                                                                                                                                                                                                
	%local _opt_creadir;                                                                                                                                                                                                                                           
                                                                                                                                                                                                                                                                
	%if %sysfunc(exist(&dset., DATA)) = 0 %then %do;                                                                                                                                                                                                               
		%log(level = E, msg = Dataset &dset. does not exist in macrofunction &sysmacroname..);                                                                                                                                                                        
		%return;                                                                                                                                                                                                                                                      
	%end;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
	%let _opt_creadir = %sysfunc(getoption(dlcreatedir));                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
	data _null_;                                                                                                                                                                                                                                                   
		set &dset. (where=(%unquote(&whr_clause.)));                                                                                                                                                                                                                  
                                                                                                                                                                                                                                                                
		if crea_path then _rc = dosubl('option dlcreatedir;');                                                                                                                                                                                                        
		else _rc = dosubl('option nodlcreatedir;');                                                                                                                                                                                                                   
                                                                                                                                                                                                                                                                
		if not missing(engine) then _rc = libname(resolve(lib), , resolve(engine), resolve(options));                                                                                                                                                                 
		else _rc = libname(resolve(lib), resolve(path), , resolve(options));                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
	run;                                                                                                                                                                                                                                                           
                                                                                                                                                                                                                                                                
	option &_opt_creadir.;                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
%mend set_libs;                                                                                                                                                                                                                                                 
