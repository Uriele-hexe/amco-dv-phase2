%macro translate(dsin=, dsout=, dsmtd=, ds_struttura_motore=, dsinWhere=1 ) / store secure ;                                                                                                                                                                    
                                                                                                                                                                                                                                                                
	/**                                                                                                                                                                                                                                                            
	* TODO: Aggiungere controlli dopo tutte le open                                                                                                                                                                                                                
	*/                                                                                                                                                                                                                                                             
                                                                                                                                                                                                                                                                
	%local                                                                                                                                                                                                                                                         
		_dsid_cnt _attr_varname _attr_vartype _attr_length _attr_fmt _attr_infmt                                                                                                                                                                                      
		_dsid _vartype _informat _varname _map_varname _map_type _map_value                                                                                                                                                                                           
		_elements _element _i                                                                                                                                                                                                                                         
	;                                                                                                                                                                                                                                                              
                                                                                                                                                                                                                                                                
	%* Aggiungo vartype per ogni variabile recuperandolo dalla tabella di destinazione (input motore);                                                                                                                                                             
	proc contents                                                                                                                                                                                                                                                  
		data = &ds_struttura_motore.                                                                                                                                                                                                                                  
		out = _cnt_ (                                                                                                                                                                                                                                                 
			keep= name type length varnum format formatl formatd informat informl informd                                                                                                                                                                                
			rename=(name=varname type=_vartype_)                                                                                                                                                                                                                         
		) noprint;                                                                                                                                                                                                                                                    
	run;                                                                                                                                                                                                                                                           
	data _cnt_;                                                                                                                                                                                                                                                    
		length _informat_ _format_ $36;                                                                                                                                                                                                                               
		set _cnt_;                                                                                                                                                                                                                                                    
		if not missing(informat) then do;                                                                                                                                                                                                                             
			if informd ne 0 then _informat_ = cats(informat, informl, '.', informd);                                                                                                                                                                                     
			else _informat_ = cats(informat, informl, '.');                                                                                                                                                                                                              
		end;                                                                                                                                                                                                                                                          
		if not missing(format) then do;                                                                                                                                                                                                                               
			if formatd ne 0 then _format_ = cats(format, formatl, '.', formatd);                                                                                                                                                                                         
			else _format_ = cats(format, formatl, '.');                                                                                                                                                                                                                  
		end;                                                                                                                                                                                                                                                          
	run;                                                                                                                                                                                                                                                           
	data &dsmtd. (drop=rc);                                                                                                                                                                                                                                        
		set &dsmtd.;                                                                                                                                                                                                                                                  
		length _vartype_ 8 _informat_ _format_ $36;                                                                                                                                                                                                                   
		if _n_ = 1 then do;                                                                                                                                                                                                                                           
			dcl hash                                                                                                                                                                                                                                                     
				ht(dataset: "_cnt_");                                                                                                                                                                                                                                       
				ht.definekey("varname");                                                                                                                                                                                                                                    
				ht.definedata("_vartype_", "_informat_", "_format_");                                                                                                                                                                                                       
				ht.definedone();                                                                                                                                                                                                                                            
		end;                                                                                                                                                                                                                                                          
		call missing(_vartype_, _informat_, _format_);                                                                                                                                                                                                                
		rc = ht.find();                                                                                                                                                                                                                                               
	run;                                                                                                                                                                                                                                                           
	                                                                                                                                                                                                                                                               
	proc sort data=_cnt_; by varnum; run;                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
	data _out_ (                                                                                                                                                                                                                                                   
		drop = _dsidin_ _dsidlkp_ _fetch_ _msg_                                                                                                                                                                                                                       
	);                                                                                                                                                                                                                                                             
		length _dsidin_ _dsidlkp_ _fetch_ 8 _msg_ $500;                                                                                                                                                                                                               
		attrib                                                                                                                                                                                                                                                        
                                                                                                                                                                                                                                                                
			%let _dsid_cnt = %sysfunc(open(_cnt_));                                                                                                                                                                                                                      
			%do %while(%sysfunc(fetch(&_dsid_cnt.))=0);                                                                                                                                                                                                                  
				%let _attr_varname = %sysfunc(getvarc(&_dsid_cnt., %sysfunc(varnum(&_dsid_cnt., varname))));                                                                                                                                                                
				%let _attr_vartype = %sysfunc(getvarn(&_dsid_cnt., %sysfunc(varnum(&_dsid_cnt., _vartype_))));                                                                                                                                                              
				%let _attr_length = %sysfunc(getvarn(&_dsid_cnt., %sysfunc(varnum(&_dsid_cnt., length))));                                                                                                                                                                  
				%let _attr_fmt = %sysfunc(getvarc(&_dsid_cnt., %sysfunc(varnum(&_dsid_cnt., _format_))));                                                                                                                                                                   
				%let _attr_infmt = %sysfunc(getvarc(&_dsid_cnt., %sysfunc(varnum(&_dsid_cnt., _informat_))));                                                                                                                                                               
                                                                                                                                                                                                                                                                
				%if %sysevalf( &_attr_vartype. = 2, boolean ) %then %let _attr_length = $&_attr_length.;                                                                                                                                                                    
                                                                                                                                                                                                                                                                
				&_attr_varname.		length=&_attr_length.                                                                                                                                                                                                                      
				%if %is_empty(%bquote(&_attr_fmt.))=0 %then %do;                                                                                                                                                                                                            
					format=&_attr_fmt.                                                                                                                                                                                                                                         
				%end;                                                                                                                                                                                                                                                       
				%if %is_empty(%bquote(&_attr_infmt.))=0 %then %do;                                                                                                                                                                                                          
					informat=&_attr_infmt.                                                                                                                                                                                                                                     
				%end;                                                                                                                                                                                                                                                       
                                                                                                                                                                                                                                                                
			%end;                                                                                                                                                                                                                                                        
			%let _dsid_cnt = %sysfunc(close(&_dsid_cnt.));                                                                                                                                                                                                               
		;                                                                                                                                                                                                                                                             
                                                                                                                                                                                                                                                                
		_dsidin_ = open("&dsin. (where=(&dsinWhere.))");                                                                                                                                                                                                              
		if _dsidin_ = 0 then do;                                                                                                                                                                                                                                      
			_msg_ = sysmsg();                                                                                                                                                                                                                                            
			put _msg_;                                                                                                                                                                                                                                                   
			stop;                                                                                                                                                                                                                                                        
		end;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
		do while(fetch(_dsidin_)=0);                                                                                                                                                                                                                                  
		                                                                                                                                                                                                                                                              
			%let _dsid = %sysfunc(open(&dsmtd.));                                                                                                                                                                                                                        
			%do %while(%sysfunc(fetch(&_dsid.))=0);                                                                                                                                                                                                                      
				%****************************************************************************                                                                                                                                                                               
				* _vartype contiene il tipo della variabile della tabella di input al motore                                                                                                                                                                                
				*****************************************************************************;                                                                                                                                                                              
				%let _vartype = %sysfunc(getvarn(&_dsid., %sysfunc(varnum(&_dsid., _vartype_))));                                                                                                                                                                           
				%****************************************************************************                                                                                                                                                                               
				* _informat contiene informat della variabile della tabella di input al motore                                                                                                                                                                              
				*****************************************************************************;                                                                                                                                                                              
				%let _informat = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., _informat_))));                                                                                                                                                                         
				%****************************************************************************                                                                                                                                                                               
				* _varname contiene il nome della variabile della tabella di input al motore                                                                                                                                                                                
				*****************************************************************************;                                                                                                                                                                              
				%let _varname = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., VARNAME))));                                                                                                                                                                             
				%*******************************************************                                                                                                                                                                                                    
				* _map_varname contiene:                                                                                                                                                                                                                                    
				* ----------------------                                                                                                                                                                                                                                    
				*   - Nulla se _map_type = CONST                                                                                                                                                                                                                            
				*   - Nulla o il nome della variabile da passare come input alla funzione sas se _map_type = SAS_FUNC                                                                                                                                                       
				*   - Nulla se _map_type = LKP                                                                                                                                                                                                                              
				*   - Il nome della variabile da cui prendere il valore se _map_type = COPY                                                                                                                                                                                 
				********************************************************;                                                                                                                                                                                                   
				%let _map_varname = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., MAP_VARNAME))));                                                                                                                                                                     
				%*************************************************************************************************************                                                                                                                                              
				* _map_type pu√≤ assumere i seguenti valori:                                                                                                                                                                                                                
				* -----------------------------------------                                                                                                                                                                                                                 
				*   - CONST: Inseriamo un valore costante                                                                                                                                                                                                                   
				*   - SAS_FUNC: Chiamiamo una funzione sas                                                                                                                                                                                                                  
				*   - LKP: Usiamo una lookup per trascodificare il valore di partenza                                                                                                                                                                                       
				*   - COPY: Copiamo il valore da una variabile in input                                                                                                                                                                                                     
				**************************************************************************************************************;                                                                                                                                             
				%let _map_type = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., MAP_TYPE))));                                                                                                                                                                           
				%*******************************************************                                                                                                                                                                                                    
				* _map_value contiene:                                                                                                                                                                                                                                      
				* --------------------                                                                                                                                                                                                                                      
				*   - Valore se _map_type = CONST                                                                                                                                                                                                                           
				*   - Funzione sas se _map_type = SAS_FUNC                                                                                                                                                                                                                  
				*   - Nome della tabella di lookup se _map_type = LKP                                                                                                                                                                                                       
				*   - Nulla se _map_type = COPY                                                                                                                                                                                                                             
				********************************************************;                                                                                                                                                                                                   
				%let _map_value = %sysfunc(getvarc(&_dsid., %sysfunc(varnum(&_dsid., MAP_VALUE))));                                                                                                                                                                         
                                                                                                                                                                                                                                                                
				%if %qupcase(&_map_type.)=CONST %then %do;                                                                                                                                                                                                                  
					%if %sysevalf(&_vartype. = 2, boolean) %then %let _map_value = "&_map_value.";                                                                                                                                                                             
					%else %if %sysevalf(&_vartype. = 1, boolean) and %is_empty(&_map_value.) %then %let _map_value = .;                                                                                                                                                        
                                                                                                                                                                                                                                                                
					"&_varname."n = &_map_value.;                                                                                                                                                                                                                              
                                                                                                                                                                                                                                                                
				%end;                                                                                                                                                                                                                                                       
				%else %if %qupcase(&_map_type.)=SAS_FUNC %then %do;                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
					%if %is_empty(%bquote(&_map_varname.))=0 %then %do;                                                                                                                                                                                                        
						/*%if %sysevalf(&_vartype. = 2, boolean) %then %let _map_varname = getvarc(_dsidin_, varnum(_dsidin_, "&_map_varname."));                                                                                                                                 
						%else %let _map_varname = getvarn(_dsidin_, varnum(_dsidin_, "&_map_varname."));*/                                                                                                                                                                        
                                                                                                                                                                                                                                                                
						%let _elements = %unquote(&_map_varname.);                                                                                                                                                                                                                
						%let _element = ;                                                                                                                                                                                                                                         
						%let _i = 1;                                                                                                                                                                                                                                              
						%let _map_varname = ;                                                                                                                                                                                                                                     
						%do %until( %is_empty( %qscan(%bquote(&_elements.), &_i., %str(,)) ) );                                                                                                                                                                                   
							%let _element = %qscan(%bquote(&_elements.), &_i., %str(,));                                                                                                                                                                                             
                                                                                                                                                                                                                                                                
							%if %sysfunc(prxmatch(%str(/<.*>/), &_element.)) %then %do;                                                                                                                                                                                              
								%let _e = %substr(&_element., %index(&_element., %str(<))+1, %index(&_element., %str(>))-%index(&_element., %str(<))-1);                                                                                                                                
								%let _dsid_in_x_type = %sysfunc(open(&dsin.));                                                                                                                                                                                                          
								%let _dsin_vartype = %sysfunc(vartype(&_dsid_in_x_type., %sysfunc(varnum(&_dsid_in_x_type., &_e.))));                                                                                                                                                   
								%let _dsid_in_x_type = %sysfunc(close(&_dsid_in_x_type.));                                                                                                                                                                                              
                                                                                                                                                                                                                                                                
								%let _element = %sysfunc(prxchange(%bquote(s/<&_e.>/getvar&_dsin_vartype.(_dsidin_, varnum(_dsidin_, "&_e."))/), 1, &_element.));                                                                                                                       
							%end;                                                                                                                                                                                                                                                    
                                                                                                                                                                                                                                                                
							%if %sysevalf(&_i. ne 1, boolean) %then %let _map_varname = &_map_varname.%str(,) &_element.;                                                                                                                                                            
							%else %let _map_varname = &_element.;                                                                                                                                                                                                                    
							%let _i = %eval( &_i. + 1 );                                                                                                                                                                                                                             
						%end;                                                                                                                                                                                                                                                     
                                                                                                                                                                                                                                                                
					%end;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
					"&_varname."n = &_map_value.(%unquote(&_map_varname.));                                                                                                                                                                                                    
                                                                                                                                                                                                                                                                
				%end;                                                                                                                                                                                                                                                       
				%else %if %qupcase(&_map_type.)=LKP %then %do;                                                                                                                                                                                                              
                                                                                                                                                                                                                                                                
					_dsidlkp_ = open( "&_map_value. (where=( upcase(strip(input_value)) = upcase(strip('" || strip(getvarc(_dsidin_, varnum(_dsidin_, "&_map_varname."))) || "')) ))" );                                                                                       
					_fetch_ = fetch(_dsidlkp_); %* OCCHIO!!! Prende il primo che incontra ;                                                                                                                                                                                    
                                                                                                                                                                                                                                                                
					%if %sysevalf(&_vartype. = 2, boolean) %then %do;                                                                                                                                                                                                          
						"&_varname."n = getvarc(_dsidlkp_, varnum(_dsidlkp_, "output_value"));                                                                                                                                                                                    
					%end;                                                                                                                                                                                                                                                      
					%else %do;                                                                                                                                                                                                                                                 
						"&_varname."n = input(getvarc(_dsidlkp_, varnum(_dsidlkp_, "output_value")), &_informat.);                                                                                                                                                                
					%end;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
					_dsidlkp_ = close(_dsidlkp_);                                                                                                                                                                                                                              
                                                                                                                                                                                                                                                                
				%end;                                                                                                                                                                                                                                                       
				%else %if %qupcase(&_map_type.)=COPY %then %do;                                                                                                                                                                                                             
                                                                                                                                                                                                                                                                
					%let _dsid_in_x_type = %sysfunc(open(&dsin.));                                                                                                                                                                                                             
					%let _dsin_vartype = %sysfunc(vartype(&_dsid_in_x_type., %sysfunc(varnum(&_dsid_in_x_type., &_map_varname.))));                                                                                                                                            
					%let _dsid_in_x_type = %sysfunc(close(&_dsid_in_x_type.));                                                                                                                                                                                                 
                                                                                                                                                                                                                                                                
					%if %sysevalf(&_vartype. = 2, boolean) and %sysevalf(&_dsin_vartype. ne C, boolean) %then %do;                                                                                                                                                             
						/* TODO: Implementare logging */                                                                                                                                                                                                                          
						%put ERROR: Variabile %bquote(&_varname.) (OUT) di tipo CHAR non compatibile con variabile %bquote(&_map_varname.) (IN) di tipo NUM;                                                                                                                      
					%end;                                                                                                                                                                                                                                                      
					%if %sysevalf(&_vartype. = 1, boolean) and %sysevalf(&_dsin_vartype. ne N, boolean) %then %do;                                                                                                                                                             
						/* TODO: Implementare logging */                                                                                                                                                                                                                          
						%put ERROR: Variabile %bquote(&_varname.) (OUT) di tipo NUM non compatibile con variabile %bquote(&_map_varname.) (IN) di tipo CHAR;                                                                                                                      
					%end;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
					%if %sysevalf(&_vartype. = 2, boolean) %then %let _map_varname = getvarc(_dsidin_, varnum(_dsidin_, "&_map_varname."));                                                                                                                                    
					%else %let _map_varname = getvarn(_dsidin_, varnum(_dsidin_, "&_map_varname."));                                                                                                                                                                           
                                                                                                                                                                                                                                                                
					"&_varname."n = &_map_varname.;                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                                
				%end;                                                                                                                                                                                                                                                       
				%else %do;                                                                                                                                                                                                                                                  
					%* TODO: Implementare logging ;                                                                                                                                                                                                                            
					%put ERROR: Categoria di traduzione non gestita - map_type: &_map_type.;                                                                                                                                                                                   
					%put ERROR: Categorie accettate: [CONST|SAS_FUNC|LKP];                                                                                                                                                                                                     
					%return;                                                                                                                                                                                                                                                   
				%end;                                                                                                                                                                                                                                                       
                                                                                                                                                                                                                                                                
			%end;                                                                                                                                                                                                                                                        
			%let _dsid = %sysfunc(close(&_dsid.));                                                                                                                                                                                                                       
                                                                                                                                                                                                                                                                
			output;                                                                                                                                                                                                                                                      
		end;                                                                                                                                                                                                                                                          
		_dsidin_ = close(_dsidin_);                                                                                                                                                                                                                                   
	run;                                                                                                                                                                                                                                                           
                                                                                                                                                                                                                                                                
	proc append                                                                                                                                                                                                                                                    
		base = &dsout. data = _out_;                                                                                                                                                                                                                                  
	run;                                                                                                                                                                                                                                                           
	%check_rc(error_level=3, msg=Errore nel caricamento della tabella &dsout. per violazione di constraint.);                                                                                                                                                      
                                                                                                                                                                                                                                                                
	proc delete data=_cnt_;run;                                                                                                                                                                                                                                    
                                                                                                                                                                                                                                                                
%mend translate;                                                                                                                                                                                                                                                
