/*{                                                                                                                                                                                                                                                             
    "title": "get_value.sas",                                                                                                                                                                                                                                   
    "desc": "Macro che recupera il valore di una variabile in un dataset per una determinata condizione (estrae solo un valore).",                                                                                                                              
    "params": [                                                                                                                                                                                                                                                 
        { "dsname": "Nome del dataset." },                                                                                                                                                                                                                      
        { "variable": "Nome della variabile contenente il valore che si vuole recuperare." },                                                                                                                                                                   
        { "whr_clause": "Condivione di where valida che viene applicata sulla tabella indicata in dsname." }                                                                                                                                                    
    ],                                                                                                                                                                                                                                                          
    "return": "Restituisce il valore della variabile estratto dalla tabella (un solo valore)",                                                                                                                                                                  
    "example": "%let _alfred_height = %get_value(sashelp.class, height, upcase(name)='ALFRED')",                                                                                                                                                                
    "author": "Hexe S.p.A.",                                                                                                                                                                                                                                    
    "website": "http://www.hexeitalia.com",                                                                                                                                                                                                                     
	"email": "<info@hexeitalia.com>",                                                                                                                                                                                                                              
	"mantainers": [ "Hexe S.p.A." ]                                                                                                                                                                                                                                
}*/                                                                                                                                                                                                                                                             
                                                                                                                                                                                                                                                                
%macro get_value(dsname, variable, whr_clause ) / store secure ;                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
    %local _dsid _rc _vartype;                                                                                                                                                                                                                                  
                                                                                                                                                                                                                                                                
    %* Checks;                                                                                                                                                                                                                                                  
    %if %is_empty(&dsname.) %then %do;                                                                                                                                                                                                                          
		%log(level = E, msg = Empty parameter dsname in macrofunction &sysmacroname..);                                                                                                                                                                               
		%return;                                                                                                                                                                                                                                                      
	%end;                                                                                                                                                                                                                                                          
	%if %is_empty(&variable.) %then %do;                                                                                                                                                                                                                           
		%log(level = E, msg = Empty parameter variable in macrofunction &sysmacroname..);                                                                                                                                                                             
		%return;                                                                                                                                                                                                                                                      
	%end;                                                                                                                                                                                                                                                          
    %if %is_empty(%bquote(&whr_clause.)) %then %do;                                                                                                                                                                                                             
		%log(level = E, msg = Empty parameter whr_clause in macrofunction &sysmacroname..);                                                                                                                                                                           
		%return;                                                                                                                                                                                                                                                      
	%end;                                                                                                                                                                                                                                                          
	%if %sysfunc(exist(&dsname.)) = 0 %then %do;                                                                                                                                                                                                                   
	 	%log(level = E, msg = Dataset &dsname. does not exist.);                                                                                                                                                                                                     
		%return;                                                                                                                                                                                                                                                      
	%end;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
    %let _dsid = %sysfunc(open(&dsname. (where=( %unquote(&whr_clause.) ))));                                                                                                                                                                                   
    %if &_dsid. eq 0 %then %do;                                                                                                                                                                                                                                 
        %log(level = E, msg = %bquote(Cannot open dataset &dsname. with where clause: &whr_clause.));                                                                                                                                                           
        %goto exit;                                                                                                                                                                                                                                             
    %end;                                                                                                                                                                                                                                                       
    %let _rc = %sysfunc(fetch(&_dsid.));                                                                                                                                                                                                                        
    %if &_rc. ne 0 %then %do;                                                                                                                                                                                                                                   
        %log(level = E, msg = %bquote(Cannot read dataset &dsname. with where clause: &whr_clause.));                                                                                                                                                           
        %goto exit;                                                                                                                                                                                                                                             
    %end;                                                                                                                                                                                                                                                       
    %if %sysfunc(varnum(&_dsid., &variable.)) = 0 %then %do;                                                                                                                                                                                                    
        %log(level = E, msg = %bquote(Variable &variable. not found in dataset &dsname.));                                                                                                                                                                      
        %goto exit;                                                                                                                                                                                                                                             
    %end;                                                                                                                                                                                                                                                       
                                                                                                                                                                                                                                                                
    %* Retrieve the type of the variable (C or N) to be able to use getvarC or getvarN to get the wanted value;                                                                                                                                                 
    %let _vartype = %sysfunc(strip(%sysfunc(vartype(&_dsid., %sysfunc(varnum(&_dsid., &variable.))))));                                                                                                                                                         
                                                                                                                                                                                                                                                                
    %* Return the value of the variable;                                                                                                                                                                                                                        
    %sysfunc(strip(%sysfunc(getvar&_vartype.(&_dsid., %sysfunc(varnum(&_dsid., &variable.))))))                                                                                                                                                                 
                                                                                                                                                                                                                                                                
%exit:                                                                                                                                                                                                                                                          
    %let _dsid = %sysfunc(close(&_dsid.));                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
%mend get_value;                                                                                                                                                                                                                                                
