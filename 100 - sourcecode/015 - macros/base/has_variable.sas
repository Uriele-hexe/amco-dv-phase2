/*{                                                                                                                                                                                                                                                             
	"title": "has_variable.sas",                                                                                                                                                                                                                                   
	"desc": "Macro che controlla se esiste una variabile in un dataset.",                                                                                                                                                                                          
	"params": [                                                                                                                                                                                                                                                    
		{ "dsname": "Nome del dataset." },                                                                                                                                                                                                                            
		{ "nomevar": "Nome della variabile." }                                                                                                                                                                                                                        
	],                                                                                                                                                                                                                                                             
	"return": "0 (False) se la varibile non esiste e 1 (True) se esiste.",                                                                                                                                                                                         
	"example": "%if %has_variable(tablename, varname) %then ...",                                                                                                                                                                                                  
	"author": "Hexe S.p.A.",                                                                                                                                                                                                                                       
	"website": "http://www.hexeitalia.com",                                                                                                                                                                                                                        
	"email": "info@hexeitalia.com",                                                                                                                                                                                                                                
	"mantainers": [ "Hexe S.p.A." ]                                                                                                                                                                                                                                
}*/                                                                                                                                                                                                                                                             
                                                                                                                                                                                                                                                                
%macro has_variable(dsname, nomevar ) / store secure ;                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
	%local _dsid;                                                                                                                                                                                                                                                  
                                                                                                                                                                                                                                                                
	%if %is_empty(&dsname.) %then %do;                                                                                                                                                                                                                             
		%log(level = E, msg = Empty parameter dsname in macrofunction &sysmacroname..);                                                                                                                                                                               
		%return;                                                                                                                                                                                                                                                      
	%end;                                                                                                                                                                                                                                                          
	%if %is_empty(&nomevar.) %then %do;                                                                                                                                                                                                                            
		%log(level = E, msg = Empty parameter nomevar in macrofunction &sysmacroname..);                                                                                                                                                                              
		%return;                                                                                                                                                                                                                                                      
	%end;                                                                                                                                                                                                                                                          
	%if %sysfunc(exist(&dsname.))=0 %then %do;                                                                                                                                                                                                                     
		%log(level = E, msg = Dataset &dsname. does not exist.);                                                                                                                                                                                                      
		%return;                                                                                                                                                                                                                                                      
	%end;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
	%let _dsid = %sysfunc(open(&dsname., is));                                                                                                                                                                                                                     
	%if &_dsid. eq 0 %then %do;                                                                                                                                                                                                                                    
		%log(level = E, msg = Cannot open dataset &dsname.);                                                                                                                                                                                                          
		%goto exit;                                                                                                                                                                                                                                                   
	%end;                                                                                                                                                                                                                                                          
	%else %do;                                                                                                                                                                                                                                                     
		%if %sysfunc(varnum(&_dsid., &nomevar.)) > 0 %then 1;                                                                                                                                                                                                         
		%else 0;                                                                                                                                                                                                                                                      
	%end;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
%exit:                                                                                                                                                                                                                                                          
	%let _dsid = %sysfunc(close(&_dsid.));                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
%mend has_variable;                                                                                                                                                                                                                                             
