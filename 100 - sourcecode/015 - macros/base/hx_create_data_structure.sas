/*{                                                                                                                                                                                                                                                             
	"Descrizione": "Macro che genera la struttura di una qualsiasi tabella.",                                                                                                                                                                                      
	"Parametri": [                                                                                                                                                                                                                                                 
		{ "datamodel": "Individua il modello dati da creare" },                                                                                                                                                                                                       
        { "dsTableName": "Nome della tabella da creare se non esiste" },                                                                                                                                                                                        
        { "processTrace": "Nome della tabella che contiene la process trace" }                                                                                                                                                                                  
        { "config_file": "Macro globale che indirizza il file di configurazione" }                                                                                                                                                                              
	],                                                                                                                                                                                                                                                             
	"Return": "void: non ritorna alcune valore",                                                                                                                                                                                                                   
	"Esempio": "%hx_create_data_structure(datamodel=<datamodel>,dsTableName=<work.<nomeTabella>>);",                                                                                                                                                               
	"Autore": "Hexe S.p.A.",                                                                                                                                                                                                                                       
	"Sito web": "<http://www.hexeitalia.com>",                                                                                                                                                                                                                     
	"Email": "<info@hexeitalia.com>",                                                                                                                                                                                                                              
	"Manutentori": [ "Hexe S.p.A." ]                                                                                                                                                                                                                               
}*/                                                                                                                                                                                                                                                             
                                                                                                                                                                                                                                                                
%Macro hx_create_data_structure(datamodel=_NULL_,dsTableName=_NULL_ ) / store secure ;                                                                                                                                                                          
	%Local _func_rc _dsid _tmpStamp _whr_clause                                                                                                                                                                                                                    
			;                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                                
  %Let _whr_clause = 1;                                                                                                                                                                                                                                         
	%Let _tmpStamp   = %sysfunc(putn(%sysfunc(datetime()),datetime.));                                                                                                                                                                                             
	%Put +---[Macro: &sysmacroname.] ----------------------+;                                                                                                                                                                                                      
	%Put | Create Data Structure [&dsTableName.]				 |;                                                                                                                                                                                                            
	%Put |.................................................|;                                                                                                                                                                                                      
	%Put | Starting at: &_tmpStamp.													;                                                                                                                                                                                                                  
	%Put +-------------------------------------------------+;                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
	%Let _func_rc=-1;                                                                                                                                                                                                                                              
	%if %sysfunc(exist(&dsTableName.))=0 %then %do;                                                                                                                                                                                                                
	  %import_xlsx_sheets(%bquote(&config_file.)                                                                                                                                                                                                                   
                          ,work                                                                                                                                                                                                                                 
                          ,whr_clause=%bquote(&_whr_clause.));                                                                                                                                                                                                  
	  Proc Sql;                                                                                                                                                                                                                                                    
	  	Create Table _data_model As                                                                                                                                                                                                                                 
          Select *                                                                                                                                                                                                                                              
					 From cmn_datamodel                                                                                                                                                                                                                                        
					Where Upcase(dataModel)="%Upcase(&datamodel.)"                                                                                                                                                                                                             
					Order By idVarname                                                                                                                                                                                                                                         
					 ;                                                                                                                                                                                                                                                         
	  Quit;                                                                                                                                                                                                                                                        
		%if &sqlObs.<=0 %then %do;                                                                                                                                                                                                                                    
      %Let _func_rc=0;                                                                                                                                                                                                                                          
      %goto uscita;                                                                                                                                                                                                                                             
	  %end;                                                                                                                                                                                                                                                        
		%Let _func_rc=1;                                                                                                                                                                                                                                              
		%Let _sep =;                                                                                                                                                                                                                                                  
		Proc Sql;                                                                                                                                                                                                                                                     
         Create Table &dsTableName.                                                                                                                                                                                                                             
			    (                                                                                                                                                                                                                                                        
	    %let _dsid = %sysfunc(open(_data_model));                                                                                                                                                                                                                  
	    %do %while (%sysfunc(fetch(&_dsid.))=0);                                                                                                                                                                                                                   
          %let _varName   = %sysfunc(getvarc(&_dsid.,%sysfunc(varnum(&_dsid.,varName))));                                                                                                                                                                       
          %let _varType   = %sysfunc(getvarc(&_dsid.,%sysfunc(varnum(&_dsid.,varType))));                                                                                                                                                                       
          %let _varLabel  = %sysfunc(getvarc(&_dsid.,%sysfunc(varnum(&_dsid.,varLabel))));                                                                                                                                                                      
          %let _varFormat = %sysfunc(getvarc(&_dsid.,%sysfunc(varnum(&_dsid.,varFormat))));                                                                                                                                                                     
          &_sep.%UnQuote(&_varName.) %UnQuote(&_varType.) "%UnQuote(&_varLabel.)" format=&_varFormat.                                                                                                                                                           
          %Let _sep=%NrQuote(,);                                                                                                                                                                                                                                
	    %end;                                                                                                                                                                                                                                                      
	    %let _dsid = %sysfunc(close(&_dsid.));                                                                                                                                                                                                                     
		    );                                                                                                                                                                                                                                                        
	  Quit;                                                                                                                                                                                                                                                        
    %end;                                                                                                                                                                                                                                                       
	%Uscita:                                                                                                                                                                                                                                                       
		%*-- Write Process Trace;                                                                                                                                                                                                                                     
		%if %sysfunc(exist(&processTrace.)) %then %do;                                                                                                                                                                                                                
		  Data _null_;                                                                                                                                                                                                                                                
		  	%hx_declare_ht_pr(htTable=&processTrace.);                                                                                                                                                                                                                 
        sourceCode = Lowcase(symget("sysmacroName"));                                                                                                                                                                                                           
        stepCode	 = "Creation data model about &dsTableName.";                                                                                                                                                                                                  
        rcCode     = &_func_rc.;                                                                                                                                                                                                                                
        if rcCode = 0 Then                                                                                                                                                                                                                                      
          msgCode = "No rows regarding &dataModel. has been extracted !!!";                                                                                                                                                                                     
        else                                                                                                                                                                                                                                                    
          msgCode = ifc(rcCode=-1,"Table exist!!"                                                                                                                                                                                                               
			                         ,"Table has been created"                                                                                                                                                                                                           
					  						);                                                                                                                                                                                                                                                 
			  rc = ht.add();                                                                                                                                                                                                                                             
			  rc = ht.output(dataset:"&processTrace.");                                                                                                                                                                                                                  
		  Run;                                                                                                                                                                                                                                                        
		%end;                                                                                                                                                                                                                                                         
		%Let _tmpStamp = %sysfunc(putn(%sysfunc(datetime()),datetime.));                                                                                                                                                                                              
		%Put +---[Macro: &sysmacroname.] ----------------------+;                                                                                                                                                                                                     
		%Put | Create Data Structure [&dsTableName.]				 |;                                                                                                                                                                                                           
		%Put |.................................................|;                                                                                                                                                                                                     
		%Put | Ended at: &_tmpStamp.													;                                                                                                                                                                                                                    
		%Put +-------------------------------------------------+;                                                                                                                                                                                                     
%Mend;                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
